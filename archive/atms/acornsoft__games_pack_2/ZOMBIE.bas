
0REM /// ZOMBIE /// V3
2P.$12;F.N=1TO16;P."!"$10;N.
4P.$8"\!/"
6P.$10$8$8$8$8"-----"'
10P."YOU HAVE LANDED ON ZOMBIE ISLAND"'
12P."YOUR ONLY HOPE OF SURVIVAL"'
14P."IS TO LURE ALL THE ZOMBIES"'
16P."INTO THE SWAMP. ENTER THE"'
18P."DIRECTIONS OF YOUR MOVE AS"'
19P."INDICATED."''"PRESS RETURN TO BEGIN"
20 LINK#FFE3
40DIMP(7),Q(7),Z(63);@=0
45DOP.$12;?#E1=0;E=0;G=E;H=ABSRND%3-1;T=#8000
50!P=#01020202;P!4=#1000000
60!Q=#2020100;Q!4=#0000102
70FORN=0TO60STEP4;Z!N=0;N.
95F.J=0TO373;T?J=#FF;IFJ%32=21J=J+10
100N.
210X=A.R.%10+5;Y=A.R.%5+3
220F.C=X-1TOX+1;F.D=Y-1TOY+1;T?(D*32-33+C)=#20;N.;N.
230T?(Y*32-33+X)=#18
235U=X;V=Y
305F=1860;L=1780
310FORX=2TO11;K=32*X-33+T;FORY=2TO21
320R=RND&2047;IFR>F OR K?Y<>#FFG.a
330IFR<L K?Y=#20;G.a
340Z?G=X;Z?(G+32)=Y;G=G+1;K?Y=#1A
370aN.;N.
440GOS.t;P."   7 8 1"'
445GOS.t;P."   6 X 2"'
450GOS.t;P."   5 4 3"';GOS.u
500REM
530IN.'"YOUR MOVE"L;GOS.u
532IFL=9ANDE>0P."HYPER-SPACE AGAIN!?"';IFE>H G.y
535IFL=9;T?(V*32-33+U)=#20;V=A.R.%10+2;U=A.R.%20+2;E=E+1;G.560
540eIFL<1ORL>8 INPUT"!!YOUR MOVE"L;GOTOe
545L=L-1
550T?(V*32-33+U)=#20
555V=V+Q?L-1;U=U+P?L-1
560I=V*32-33+U
570IFT?I=#20;T?I=#18;G.f
580IFT?I=#1A PRINT"STRAIGHT INTO A ZOMBIE";GOTOg
600P."HEAD FIRST INTO THE SWAMP."'"ARE YOU BLIND TOO?"';G.810
620fF=0
625kC=Z?F;D=Z?(F+32)
630T?(C*32-33+D)=#20
640S=V-C;GOSUBs;C=C+S
650S=U-D;GOSUBs;D=D+S
660I=C*32-33+D
670IFT?I=#20G.h
680IFT?I=#1AP." ZOINK";T?(Z?F*32-33+Z?(32+F))=#1A;G.i
690IFT?I=#18;T?I=#98;GOS.u;P."YOU'RE ZAPPED BY A ZOMBIE";G.g
700PRINT" SPLASH"
710FORN=F TOG;Z?N=Z?(N+1);Z?(N+32)=Z?(N+33);N.
720G=G-1;GOTOj
730hT?I=#1A
740Z?F=C;Z?(F+32)=D
750iF=F+1
760jIFF<=G GOTOk
770IFG>=0GOTO500
780GOS.u;P."!!WELL DONE!! YOU ESCAPED"'
790PRINT"THE ZOMBIES ARE EXTINCT, BUT"'
810PRINT"YOU MAY INDULGE YOUR DEATH WISH"'"BY STARTING AGAIN"
820INPUT" YES "$256
830UNTIL ?256<>CH"Y"
835END
840gPRINT'"YUM YUM! THAT WAS TASTY."';GOTO810
850PRINT'" YOUR DEATH";GOTO810
900yP."THE FABRIC OF THE SPACE-TIME"'
910P."CONTINUUM HAS BEEN OVER-STRESSEDBY YOUR JUMPS, "
920P."AND THE ISLAND"'"HAS DISAPPEARED."';G.810
1000sIFS>0S=1
1010IFS<0S=-1
1020RETURN
2000tF.N=1TO22;P.$9;N.;R.
3000uF.N=#8180TO#81FF;?N=#20;N.
5000vP.$30;F.N=1TO12;P.$10;N.;R.
0 (C) ACORNSOFT 1980
