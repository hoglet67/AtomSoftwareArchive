
10 ?#23=#00;?#24=#82
20 DIM P(64),AA(100),CC(100),DD(100)
30 FOR N=1 TO 100
40 DIM J(20);AA(N)=J;$AA(N)=""
50 CC(N)=0;DD(N)=0
60 NEXT N
70 N=1;CC(1)=999
100aPRINT $12,$3,$15
110 PRINT "MENU OF OPTIONS"''
120 PRINT"1=READ DATA FILE"'
130 PRINT"2=WRITE DATA FILE"'
140 PRINT"3=TRIAL BALANCE"'
150 PRINT"4=AMEND ACCOUNT CODES"'
160 PRINT"5=POST DATA"'
170bINPUT"ANSWER WITH NUMBER "A
180 IF A<1 OR A>5 THEN G.b
190 GOTO (100+100*A)
200 A=FIN""
210 N=1;SGET A,P
220 CC(N)=GET A;DD(N)=GET A;SGET A,AA(N)
240 IF CC(N)<>999 THEN N=N+1;G.220
280 INPUT"REWIND TAPE & REMOVE"$P
290 G.a
300 A=FOUT""
310 SPUT A "START OF TAPE";WAIT
320 FOR Q=1 TO N
330 PUT A CC(Q);WAIT
340 PUT A DD(Q);WAIT
350 SPUT A AA(Q);WAIT
360 NEXT
380 INPUT "REWIND TAPE & REMOVE"$P
390 G.a
400 FOR X=1 TO 7;PRINT';NEXT
410 @=1;INPUT"SCREEN ONLY Y/N"$P;IF $P="Y" THEN PRINT $14;G.d
415 PRINT $2,$15
420dPRINT';T=0;G=0;C=0;X=1
430cIF CC(X)=999 THEN PRINT"GRAND TOTAL";G.480
440 PRINT CC(X)," ",$AA(X);P!0=DD(X);P!4=20;GOSUB 1200;PRINT'
450 GOSUBe
460 X=X+1
470 G.c
480 P!0=G;P!4=20;GOSUB 1200
490 PRINT';INPUT $P;G.a
500 PRINT $12
505gINPUT "CODE - 0 TO END"A
510 IF A<0 OR A>999 THEN G.g
515 IF A=0 THEN G.a
520 C=0;FOR Q=1 TO N;IF CC(Q)=A THEN C=Q
523 NEXT
525 IF C<>0 THEN G.i
530 INPUT "NEW CODE Y/N"$P
535 IF $P="Y" THEN GOSUB h;G.i
540 G.g
545iPRINT "OLD TITLE"$AA(C)'
550 INPUT"NEW TITLE"$P
555 IF LENP>16 THEN PRINT"TOO LONG"';G.i
560 $AA(C)=$P
565 G.g
600 PRINT $12,$2;@=1
610 T=0
620jPRINT"REVENUE/CREDITS=C"'
630 PRINT"EXPENSES/DEBITS=D"'
640 INPUT $P
650 IF $P="C" OR $P="D" THEN G.660
655 G.j
660 S=1;IF $P="C" THEN S=-1
670nGOSUBk;IF A=0 THEN G.m
680 GOSUBl
690 INPUT"NARR"$P
700 DD(C)=DD(C)+A*S;T=T+A
710 G.n
720mPRINT"BATCH TOTAL   ";P!0=T;P!4=20;GOSUB 1200;PRINT'
730 PRINT"CONTROL ACCOUNT "
740 GOSUBk
750 S=0-S;T=T*S
760 DD(C)=DD(C)+T
770 INPUT"MORE"$P;IF $P="Y" THEN G.600
780 G.a
800kINPUT"CODE"A;IF A=0 THEN R.
810 C=0;FOR Q=1 TO N;IF CC(Q)=A THEN C=Q
815 NEXT
820 IF C=0 THEN PRINT"NO SUCH CODE"';G.k
830 PRINT $AA(C)'
840 R.
850lFINPUT"AMOUNT"%A
870 %A=%A*100;A=%A
880 R.
1000eREM TEST FOR CONTROL BREAK
1010 T=T+DD(X);C=C+1;G=G+DD(X)
1020 IF CC(X)/10=CC(X+1)/10 THEN R.
1030 IF C=1 THEN PRINT';G.f
1040 PRINT"SUBTOTAL";P!0=T;P!4=20;GOSUB 1200;PRINT''
1050fT=0;C=0
1060 IF(CC(X+1)/10<50)OR(CC(X)/10>49)THEN R.
1070 PRINT"PROFIT/LOSS";P!0=G
1080 P!4=20;GOSUB1200;PRINT''';R.
1200 DO PRINT" ";UNTIL COUNT>=P!4
1210 P!4=0;P!8=ABS(P!0)
1220 DO P!4=(P!4)+1;P!8=(P!8)/10
1230 UNTIL P!8<100
1240 P!4=6-(P!4);P!8=ABS(P!0)
1250 DO P!4=(P!4)-1;PRINT" ";UNTIL P!4<=0
1260 PRINT (P!8)/100,".";P!8=(P!8)%100
1270 IF P!8<10 THEN PRINT"0"
1280 PRINT P!8
1290 IF (P!0)<0 THEN PRINT"CR"
1300 R.
2000hREM INSERT INTO TABLE
2010 Q=0
2020 DO Q=Q+1
2030 UNTIL CC(Q)>A
2040 FOR R=N TO Q STEP -1
2050 $AA(R+1)=$AA(R)
2060 CC(R+1)=CC(R)
2070 DD(R+1)=DD(R)
2080 NEXT
2090 $AA(Q)=""
2100 CC(Q)=A
2110 DD(Q)=0
2120 N=N+1;C=Q;R.
3000 END
