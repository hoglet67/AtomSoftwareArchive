
10 DIM P(240),T(64);FDIM %AA(52)
20 FOR A=0 TO 52;%AA(A)=0;NEXT
30 %AA(0)=1
100 PRINT$12
110 PRINT"MENU OF OPTIONS"''
120 PRINT"1=READ DATA FILE"'
130 PRINT"2=WRITE DATA FILE"'
140 PRINT"3=REVIEW FIGURES"'
150 PRINT"4=AMEND FIGURES"'
160 PRINT"5=AMEND TITLE"'
180 PRINT"6=SHUFFLE DATA"'
190bINPUT"ANSWER WITH NUMBER "A
200 IF A<1 OR A>6 THEN G.b
210 GOTO(200+100*A)
300 A=FIN"";SGET A,T
310 SGET A,T
320 FOR N=0 TO 52
330 %AA(N)=FGET A
340 NEXT
350 PRINT"REWIND TAPE"'
360 G. 100
400 A=FOUT"";SPUT A,"START"
410 SPUT A,T
420 FOR N=0 TO 52
430 FPUT A,%AA(N);WAIT
440 NEXT
450 PRINT "REWIND TAPE"'
460 G.100
500 FOR N=1 TO 6;PRINT';NEXT
510 PRINT $14;W=%AA(0);@=6
520 FOR N=1 TO 52
530 PRINT "WEEK",W,"   ";FPRINT%AA(N)'
540 W=W+1;IF W>52 THEN W=W-52
550 NEXT
560 PRINT $15
570 INPUT"PRESS RETURN WHEN READY"$P
580 G.100
600 PRINT $12
610hINPUT"WEEK NO-0 TO END "W
620 IF W<0 OR W>52 THEN G.h
630 IF W=0 THEN G.100
640 FINPUT "SALES FIGURES "%W
650 FIF%W<0 THEN PRINT"OH!"';G.h
660 W=W-%(%AA(0))+1
670 IF W<1 THEN W=W+52
680 %AA(W)=%W
690 G.h
700 PRINT $12
710 PRINT "CURRENT TITLE IS ",$T'  
720mINPUT$T
730 IF LENT>30 THEN PRINT "TOO LONG "';G.m
740 G.100
800 PRINT $12
910 PRINT "THIS OPTION ALLOWS YOU TO LOSE"'
920 PRINT"UP TO 13 WEEKS OF THE EARLIEST"'
930 PRINT "DATA AND ROLL UP THE REMAINDER"'
940 INPUT "DO YOU REALLY WANT TO DO IT Y/N"$P
950 IF $P="Y"THEN G.970
960 G.100
970 INPUT"HOW MANY WEEKS 1-13"A
980 W=%AA(0)
990 W=W+A;IF W>52 THEN W=W-52
1000 %AA(0)=W
1010 FOR N=1 TO 52-A
1020 %AA(N)=%AA(A+1)
1030 A=A+1
1040 NEXT N
1050 FOR W=N TO 52
1060 %AA(W)=0
1070 NEXT W
1080 G.100
