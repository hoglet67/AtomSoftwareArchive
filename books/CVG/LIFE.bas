  20 REM LIFE
  30 REM BY TIM JACKSON, MAY 81
  40 ?#E1=0
  50 GOSUBl
  60 A=#3800;D=A+320;B=D+320
  70 REM A HOLDS PATTERN
  80 REM D HOLDS NEIGHBR COUNT
  90 REM INIT.PATT.SAVED IN B
 100 G=0
 110 DIM LL0,SS13,GG13
 120 FOR I=0 TO13;SSI=-1;GGI=-1;NEXT
 130 DIM W64
 140 P.$21
 150 GOSUBm;GOSUBm
 160 P.$6
 170 GOSUBi
 180rFOR I=0 TO 319 STEP 4; A!I=0; NEXT
 190 GOSUBw;GOSUBh;GOSUBb
 200 REM GENERATE PATTERN
 210sC=0;REM CURSOR POSN
 220 GOSUBc
 230 DO
 240 GOSUBg
 250 GOSUBc
 260 REM CHECK CRSR CTRLS
 270 IF G=7 C=C+20; IF C>319 C=C-320
 280 IF G=6 C=C+1; IF C%20=0 C=C-20
 290 IF G=107 C=C-20; IF C<0 C=C+320
 300 IF G=106 C=C-1; IF C%20=19OR C=-1 C=C+20
 310 GOSUBc
 320 REM IF SP, INVERT CELL
 330 IF G=0 PLOT 14,(C%20*2+22),(C/20*2+2); A?C=A?C:1
 340 FOR I=0 TO 300;NEXT
 350 UNTIL G=13;REM UNTIL RTN
 360 FOR I=0 TO 319 STEP 4; B!I=A!I; NEXT
 370 GOSUBc
 380 GOSUBj
 390 @=4;N=0
 400 PRINT N,$8$8$8$8
 410tDO
 420 FOR I=0 TO 319 STEP 4;D!I=0;NEXT
 430 REM SCAN CELLS
 440 LINK SS0
 450 REM GENERATE NEXT GEN.
 460 LINK GG0
 470 N=N+1;IF N>9999 N=0
 480 PRINT N,$8$8$8$8
 490 I=0
 500 DO I=I+1
 510 LINK LL0
 520 UNTIL I>V OR G<255
 530 UNTIL G<255
 540 P.$30'''''"PRESS G"'"TO GO ON"'" --OR--"'
 550 P."PRESS P"'"FOR YOUR"'"ORIGINAL"'"PATTERN"
 560 P.'" --OR--"'"PRESS S"'"TO START"'"AFRESH"
 570qGOSUBk
 580 IF G=39 GOSUBj;GOTOt
 590 IF G=51 GOTOr
 600 IF G<>48 GOTOq
 610 GOSUBw;GOSUBh;GOSUBb
 620 J=#2020202
 630 FOR I=0 TO 319 STEP 4
 640 A!I=B!I; D!I=J
 650 NEXT
 660 LINK GG0
 670 GOTOs
1000 REM CURSOR
1010cMOVE(C%20*2+23),(C/20*2)
1020 FOR I=1 TO 3
1030 PLOT 8,-2,1
1040 PLOT 2,2,0
1050 NEXT I
1060 RETURN
2000 REM KEYBD INPUT
2010kDO LINK LL0; U.G<255;R.
2020 REM KEYBD INPUT & DETECT CRSR CTRLS
2030gGOSUBk
2040 IF G<6 OR G>7 RETURN
2050 IF ?#B001&#80=0 G=G+100
2060 RETURN
3000 REM HEADING
3010hCLEAR 0
3020 MOVE 0,47;DRAW 0,41; DRAW 4,41
3030 MOVE 6,41;DRAW 6,47
3040 MOVE 8,41;DRAW 8,47;DRAW 12,47;MOVE 8,44;DRAW 11,44
3050 MOVE 18,47;DRAW 14,47;DRAW 14,41;DRAW 18,41
3060 MOVE 14,44;DRAW 17,44;
3070 MOVE 0,39;DRAW 18,39
3080 RETURN
3090bMOVE 62,0;DRAW 62,34;DRAW 20,34;DRAW 20,0;DRAW 62,0
3100 P.$30'''''"USE THE"'"CURSOR"'"AND THE"'
3110 P."SPACEBAR"'"TO CREATE"'"& DELETE"'"CELLS,"'
3120 P."AND THEN"'"PRESS"'"RETURN"'"TO START"
3130 RETURN
3140jP.$30'''';FOR I=8 TO 18;P.'"          ";NEXT
3150 PRINT$30''''''''"PRESS"'"SPACEBAR"'"TO STOP"
3160 P.$30$10$10$9$9$9$9$9$9$9$9$9$9$9$9"GENERATION NO:"
3170 RETURN
4000mP=#2800;K=#80;F=#5A;E=#3FE
4010[
4020 :LL0
4030 JSR #FE71; STY #328; RTS
4040\SET UP GRAPHICS-TOP RH CORNER
4050:GG0 LDA @0
4060 STA F+1;STA F+3
4070 LDA @60; STA F
4080 LDA @32; STA F+2
4090\A+256 IN K,D+256 IN K+2
4100 LDA @A&#FF; STA K
4110 LDA @A&#FF00/256+1; STA K+1
4120 LDA @D&#FF; STA K+2
4130 LDA @D&#FF00/256+1; STA K+3
4140 LDY @64
4150 LDX @1
4160:GG1 DEY
4170 TYA;PHA;TXA;PHA
4180 JSR GG2  DO NEXT GEN.
4190 PLA;TAX;PLA;TAY
4200 DEC F; DEC F
4210 LDA @20; CMP F; BNE GG3
4220 DEC F+2; DEC F+2
4230 LDA @60; STA F
4240:GG3
4250 TYA;BNE GG1
4260 DEX;BMI GG4
4270 DEC K+1;DEC K+3
4280 JMP GG1
4290:GG4 RTS
4300:GG2
4310 LDA (K+2),Y
4320 CMP @3;BEQ GG5 BIRTH
4330 CMP @2;BNE GG6 DEAD
4340 LDA (K),Y
4350 CMP @1; BEQ GG5; STAY ALIVE
4360:GG6
4370 LDA @0
4380:GG7
4390 STA (K),Y; STA F+4
4400 JMP (E)
4410:GG5
4420 LDA @1; BPL GG7
4430:SS0 LDA @(A+279)&#FF00/256
4440 STA K+1
4450 LDA @(A+279)&#FF;STA K
4460 LDA @(D+279)&#FF00/256;STA K+3
4470 LDA @(D+279)&#FF;STA K+2
4480 LDX @15
4490:SS2 LDY @40
4500:SS3 JSR SS1
4510 DEY; CPY @20; BNE SS3
4520 SEC; LDA K; SBC @20; STA K
4530 BCS SS4; DEC K+1; SEC
4540:SS4 LDA K+2;SBC @20;STA K+2
4550 BCS SS5; DEC K+3
4560:SS5 DEX; BPL SS2
4570 RTS
4580:SS1 LDA (K),Y;BEQ SS6
4590 STY K+4
4600 CPX @0; BEQ SS7
4610 TYA;SEC;SBC@21;TAY;JSR SS8
4620:SS7 LDY K+4;CPY@21;BEQSS9;DEY
4630 JSR SS13;INY;CPY@40;BEQSS10
4640:SS9 INY;JSR SS13;DEY
4650:SS10 CPX@15;BEQ SS11
4660 TYA;CLC;ADC@19;TAY;JSR SS8
4670:SS11 LDY K+4;RTS
4680:SS8 LDA K+4;CMP@21;BEQSS12
4690 JSR SS13
4700:SS12 INY;JSR SS13
4710 LDA K+4;CMP@40;BEQ SS6
4720 INY;JSR SS13
4730:SS6 RTS
4740:SS13 LDA(K+2),Y;CLC;ADC@1;STA(K+2),Y;RTS
4750];RETURN
6000lGOSUBh;P.$30''''
6010 P."THIS PROGRAM SIMULATES THE"'
6020 P."EVOLUTION OF A COLONY OF CELLS"'
6030 P."OVER SUCCESSIVE GENERATIONS,"'
6040 P."IN ACCORDANCE WITH THE"'"FOLLOWING RULES."'''
6050 GOS.i;GOS.h;P.$30''''
6060 P."A LIVE CELL WILL LIVE IF IT HAS JUST 2 OR 3 NEIGHBOURS"
6070 P.","'"HORIZONTALLY, VERTICALLY OR"'"DIAGONALLY."'
6080 P."WITH OVER 3 NEIGHBOURS;IT WILL"'
6090 P."DIE FROM OVERCROWDING."'
6100 P."WITH LESS THAN 2, IT WILL DIE"'"OF LONLINESS."'
6110 P."IF AN EMPTY LOCATION HAS EXACTLY3 NEIGHBOURS, "
6120 P."A NEW CELL WILL BEBORN."'
6130 GOSUBi;GOSUBh;P.$30''''
6140 P."   YOU MUST SET UP THE INITIAL"'"PATTERN OF CELLS, "
6150 P."USING THE"'"DOUBLE-HEADED ARROW KEYS AT THE LEFT OF "
6160 P."THE KEYBOARD TO MOVE"'"THE CURSOR (WHITE SQUARE)."'
6170 P."   PRESS SHIFT TO REVERSE THE"'"CURSOR MOVEMENT."'
6180 P."   PRESS SPACEBAR TO CREATE OR"'
6190 P."DELETE A CELL AT THE CURSOR"'"POSITION."''
6200 RETURN
7000iP."**PRESS SPACEBAR TO CONTINUE**";LINK#FFE3;RETURN
8000wGOSUBh;P.$30''''''
8010 PRINT"WHEN GENERATION SPEED WOULD"'"YOU LIKE?"'
8020vPRINT"TYPE A NUMBER BETWEEN 0 (FAST)"'"AND 9 (SLOW)"
8030 INPUT", AND PRESS RETURN"',$W
8040 IF LENW>1 GOTOv
8050 IF ?W>#39 OR ?W<#30 GOTO v
8060 V=(?W-#30)*100
8070 RETURN
