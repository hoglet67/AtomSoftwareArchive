   10 REM Simultaneous Equations
   50 INPUT"NUMBER OF EQUATIONS="N
   60 I=N*N;J=N*(N+1)
   65 DIM AA(I),CC(J),II(N)
   70 @=0;FOR I=1TON;FOR J=1TO N+1
   80 PRINT"C("I","J")=";INPUT C
   90 CC((I-1)*(N+1)+J)=C;NEXT J;NEXT I
  100 L=N+1;GOSUB c;E=D;M=1-2*(N%2)
  110 PRINT'"SOLUTION:"'
  112 IF E<0 E=-E;M=-M
  115 IF E=0 PRINT"DEGENERATE!"';END
  120 FOR L=1TON;GOSUB c
  125 PRINT"X("L")="
  130 A=M*D;B=E;DO A=A%B
  140 IF ABS(B)>ABS(A) THEN T=B;B=A;A=T
  150 UNTIL B=0;A=ABS(A)
  151 P.(M*D)/A;IF E/A<>1 PRINT"/"E/A
  155 M=-M;PRINT';NEXT L;END
  160cFOR I=1TON;FOR J=1TON;K=I*N-N+J
  170 IF J<L AA(K)=CC(K+I-1)
  180 IF J>=L AA(K)=CC(K+I)
  190 NEXT J;NEXT I
  200dD=0;F=1;S=1
  210 FOR J=1TON;II(J)=J;F=F*J;NEXT J
  215 GOSUB f
  220 FOR H=2TOF;GOSUB e;NEXT H;RETURN
  230eI=N-1;J=N
  240gIF II(I)>=II(I+1) I=I-1;GOTO g
  250hIF II(I)>=II(J) J=J-1;GOTO h
  260 GOSUB i;I=I+1;J=N;IF I=J GOTO f
  270 DO GOSUB i;I=I+1;J=J-1;UNTIL I>=J
  280fP=1;FOR K=1TON;P=P*AA(N*K-N+II(K))
  290 NEXT K;D=D+S*P;RETURN
  300iK=II(I);II(I)=II(J);II(J)=K
  310 S=-S;RETURN

