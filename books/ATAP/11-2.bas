    1 REM Sierpinski Curve
   10 INPUT"MODE"O
   15 INPUT"SIZE"K
   20 CLEARO
   30 S=5
   40 J=1
   50 FOR I=1 TO 5
   60 J=J*2;D=K/J/4
   70 X=K-5*D; Y=K-2*D
   80 T=1; MOVE X,Y
   90 X=X+D; A=J; B=J; GOTO s
  100aIF A=J AND B=J GOTO z
  110sP=J; Q=A; R=B
  120vIF P<2 GOTO z
  130 IF P=2 GOSUB o; GOTO a
  140 P=P/2
  150 IF Q<P OR P+1<Q GOTO n
  170 IF R<P OR P+1<R GOTO n
  190 GOSUB c; GOTO a
  200nIF Q>=P THEN Q=Q-P
  210 IF R>=P THEN R=R-P
  220 GOTO v
  230zREM end of loop
  240 FOR N=1 TO 1000;NEXT
  250 CLEARO
  260 NEXT I
  270 END
 1000cGOTO(1000+100*T)
 1100 X=X+D
 1105 PLOTS,X,Y
 1110 X=X+D;Y=Y+D;PLOTS,X,Y
 1120 Y=Y+D;B=B+1;T=4;RETURN
 1200 Y=Y-D
 1205 PLOTS,X,Y
 1210 X=X+D;Y=Y-D;PLOTS,X,Y
 1220 X=X+D;A=A+1;T=1;RETURN
 1300 X=X-D
 1305 PLOTS,X,Y
 1310 X=X-D;Y=Y-D;PLOTS,X,Y
 1320 Y=Y-D;B=B-1;T=2;RETURN
 1400 Y=Y+D
 1405 PLOTS,X,Y
 1410 X=X-D;Y=Y+D;PLOTS,X,Y
 1420 X=X-D;A=A-1;T=3;RETURN
 2000oGOTO(2000+100*T)
 2100 X=X+D;PLOTS,X,Y
 2110 X=X+D;Y=Y+D;PLOTS,X,Y
 2120 X=X+D;Y=Y-D;GOTO 1305
 2200 Y=Y-D;PLOTS,X,Y
 2210 X=X+D;Y=Y-D;PLOTS,X,Y
 2220 X=X-D;Y=Y-D;GOTO 1405
 2300 X=X-D;PLOTS,X,Y
 2310 X=X-D;Y=Y-D;PLOTS,X,Y
 2320 X=X-D;Y=Y+D;GOTO 1105
 2400 Y=Y+D;PLOTS,X,Y
 2410 X=X-D;Y=Y+D;PLOTS,X,Y
 2420 X=X+D;Y=Y+D;GOTO 1205

