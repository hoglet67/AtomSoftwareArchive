    1 REM Animals
   10 REM Load Tree
   20 F=FIN""
   23 DO UNTIL BGET F=#AA
   25 FOR T=TOP TO TOP+GET F
   30 ?T=BGET F; NEXT T
   35 DO X=TOP
   40 PRINT'"ARE YOU THINKING OF AN ANIMAL"
   45 GOSUB q
   48 IF Q=0 THEN GOSUB z; END
   50 DO PRINT $X+1
   60 GOSUB q
   65 P=X+LENX+1+Q; X=!P+TOP
   70 UNTIL ?X<>CH"*"
   75 PRINT"IS IT " $X
   80 GOSUB q
   85 IF Q=4 PRINT "HO-HO";UNTIL 0
   90 DO INPUT"WHAT WERE YOU THINKING OF"$T
   95 UNTIL LEN T>2
   98 L=T; GOSUB s
  100 PRINT"   TELL ME A QUESTION "
  110 PRINT"THAT WILL"'"DISTINGUISH "
  120 PRINT "BETWEEN " $L " AND " $X '
  130 $T="*"; R=T+1
  140 INPUT $R; !P=T-TOP; GOSUB s
  145 K=T; T=T+8; GOSUB j
  150 GOSUB q
  160 K!Q=X-TOP; K!(4-Q)=L-TOP
  170 UNTIL 0
 1000qINPUT $T
 1010 IF ?T=CH"Y"THEN Q=4; RETURN
 1020 IF ?T=CH"Q"THEN END
 1030 Q=0; RETURN
 2000j$T=$R; A=1
 2010 DO A=A+1
 2020 V=T?(A+4); $T+A+4=""
 2030 IF $T+A=" IT " UNTIL 1; GOTO k
 2035 T?(A+4)=V
 2040 UNTIL A=LEN T-5
 2100 PRINT"WHAT WOULD THE ANSWER BE"'
 2110 PRINT"FOR " $X
 2120 RETURN
 2150kT?(A+4)=V; $T+A+1=""
 2160 PRINT $T,$X,$T+A+3
 2170 RETURN
 3000sT=T+LEN T+1; RETURN
 9000 REM Set-Up File
 9010 T=TOP; $T="*DOES IT HAVE FOUR LEGS"
 9015 GOSUB s; P=T; T=T+8; !P=T-TOP
 9020 $T="A CROW"; GOSUB s; P!4=T-TOP
 9025 $T="A DOG"; GOSUB s
 9100zREM Save Tree
 9110 F=FOUT ""
 9112 BPUT F,#AA; WAIT
 9115 PUT F,(T-TOP-1)
 9120 FOR N=TOP TO T-1
 9130 BPUT F, ?N
 9140 NEXT N
 9150 RETURN

