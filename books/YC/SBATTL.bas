
3B=0;F=0;Q=0
6P.#12';INPUT" ALIENS "I
7INPUT" LASERS "L
10DIM K(2),KK(3);X=128/2;Y=192/2
11LET U=ABSRND%128;LET N=ABSRND%192
20FOR J=0 TO 1;DIM P(-1)
30[;:KK0 LDY @#3B;CLC;LDA @#20;:KK1 LDX @10;
31:KK2 BIT #B001;BEQ KK3
40INC #B000;DEY;DEX;BNE KK2;LSRA;:KK3 PHP;PHA
45LDA #B000;AND @#F0
46STA #B000;PLA;PLP;BNE KK1;STY K
50RTS;];NEXT J
60CLEAR 3
70LINK KK0
71GOSUB799
75F=F+1
80IF ?K=7 GOTO 199
90IF ?K=6 GOTO 399
100IF ?K=29GOTO 499
110IF ?K=4 GOTO 299
120IF ?K=0 GOTO 599
125U=U+(RND%3);N=N+(RND%5);IF N<0 THEN N=0
126IF N>192 THEN N=192
127IF U<0 THEN U=0
128IF U>128 THEN U=128
130GOTO70
140MOVE(X-15),Y;PLOT 7,(X+15),Y;MOVE X,(Y-20);PLOT 7,X,(Y+20)
145RETURN
150MOVE(X-15),Y;DRAW (X+15),Y;MOVE X,(Y-20);DRAW X,(Y+20)
170GOTO 70
199GOSUB 140;Y=Y-10;IF Y<0 THEN Y=0
200GOTO 150
299GOSUB 140;Y=Y+10;IF Y>192 THEN Y=192
310GOTO 150
399GOSUB 140;X=X-5;IF X<0 THEN X=0
410GOTO150
499GOSUB 140;X=X+5;IF X>128 THEN X=128
510GOTO150
599Q=Q+1;IF Q>L GOTO 900
600P.$7; FOR R=1 TO 15 STEP 2
610MOVE (X+R),0;DRAW X,Y;MOVE (115+R),0;DRAW X,Y;NEXT R;P.$7
615FOR R=1 TO 15 STEP 2
620MOVE (X+R),0;PLOT 7,X,Y;MOVE (115+R),0;PLOT 7,X,Y;NEXT R
650IF X> (U+3) GOTO 70
651IF X< (U-3) GOTO 70
652IF Y> (N+5) GOTO 70
653IF Y< (N-5) GOTO 70
655FOR C=1 TO 60;H=RND%30;V=RND%50;PLOT 13,(X+H),(V+Y)
660NEXT C
665FOR C=1 TO 100;WAIT;NEXT C
666U=ABSRND%128;N=ABSRND%192;B=B+1
667IF B<I GOTO 70
668CLEAR 0
670P.$7" WELL DONE"'
680P.F" TURNS TAKEN TO DESTROY THE ALIENS "
681P." YOUR FINAL SCORE WAS "(100-(L-1))*(1000-F)
690GOTO 10000
700GOTO 70
799Z=13
800PLOT Z,U,N;PLOT Z,(U+1),(N+1);PLOT Z,(U+1),N
801PLOT Z,(U-1),N;PLOT Z,(U+1),N;PLOT Z,(U-2),(N-1)
802PLOT Z,(U-1),N;PLOT Z,(U+2),(N-2);PLOT Z,(U+3),(N-1)
804IF Z=15 GOTO 810
805Z=15;GOTO 800
810RETURN
900P.$12" ALL LASERS USER , YOU FAILED "
10000END
