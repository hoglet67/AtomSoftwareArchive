
0 REM THE BEST OF INTERFACE PAGE 45-46
10 GOSUB r
20 PRINT $30
30 M=#81C5;N=#81CF;O=#81D9
40 DIM AA(35);W=0;@=0;DIM U(3);L=0
50 P=#2801;PRINT $21
60[;JSR #FE71;STY #2800;RTS;];PRINT $6
70 C=#B002
80 CLEAR 0
90 GOSUB a
100 FOR S=#81E0 TO #81FF;?S=#40;NEXT S
110 X=0
120 A=X;PLOT 13,X,0
130 X=X+1
140 IF X=65 THEN GOTO 220
150 V=ABSRND%100
160 IF V>=50 THEN GOSUB b
170 LINK #2801
180 IF ?#2800=3 THEN GOTO 210
190 PLOT 15,A,0
200 GOTO 120
210 FOR S=#8000 TO #81E0 STEP 32;?S=#40;NEXT S
220 Y=0
230 B=Y;PLOT 13,0,Y
240 Y=Y+1
250 IF Y=49 THEN GOTO 100
260 V=ABSRND%100
270 IF V>=50 THEN GOSUB b
280 LINK #2801
290 IF ?#2800=1 THEN GOSUB z
300 PLOT 15,0,B
310 GOTO 230
320a REM ? INVADERS & CITIES
330 FOR R=1 TO 35
340 J=ABSRND%31+#8001
350 AA(R)=J
360 ?J=#27
370 NEXT R
380 ?N=#D7;?(N-1)=#EB;?(N+2)=#CB
390 ?M=#DF;?(M-1)=#C7;?(M+1)=#C3
400 ?O=#EA;?(O-1)=#CB;?(O+1)=#C7
410 RETURN
420b REM MOVE MISSILES
430 M=ABSRND%36
440 IF ?AA(M)<>#27 THEN GOTO 500
450 ?AA(M)=#40
460 AA(M)=AA(M)+32
470 IF ?AA(M)>#C0 AND ?AA(M)<#FF THEN L=L+1
480 ?AA(M)=#27
490 IF AA(M)>#829F THEN GOTO 550
500 RETURN
510z P=A/2+(47-B)/3*32+#8000
520 IF ?P=#40 THEN ?P=#A3;PRINT $7;?P=#40
530 IF ?P=#27 THEN ?P=#AA;PRINT $7;?P=#40;W=W+1
540 RETURN
550 PRINT $12'''"  THE END OF THE GAME."'
560 PRINT"  ********************"''"  SCORE="(W*15)-(L*10)'
570 PRINT''';INPUT"ANOTHER GO"$U
580 IF $U="YES" OR $U="Y" THEN GOTO 20
590 PRINT $12;END
600r PRINT $12"   ###CO-ORD.###"''"   (PROGRAM BY R.G.HEARD.)"'
610 PRINT''"YOU MUST DEFEND YOUR 3 CITIES"'
620 PRINT"WHICH ARE LOCATED AT THE BASE   OF THE SCREEN."'
630 PRINT"TO DO THIS YOU MUST DESTROY THE ATTACKING MISSILES."
640 PRINT"THIS IS DONE BY STOPPING THE 2 FLASHING      CURSORS"
650 PRINT":"'''"PRESS KEY TO CONTINUE";LINK #FFE3;PRINT $12
660 PRINT"TO STOP THE 'X-CO.ORD' CURSOR   PRESS ']'."'
670 PRINT"TO STOP THE Y-CO.ORD' CURSOR  PRESS '['."''
680 PRINT"WHERE THE HORIZONTAL & VERTICAL LINES FROM THESE "
690 PRINT"INTERSECT A    BOMB WILL BE DROPPED."'''
700 PRINT"n.b.YOU MUST STOP THE 'X-' CURSOR FIRST."''
710 PRINT"GOOD LUCK!";FOR K=1 TO 20000;NEXT K;RETURN
