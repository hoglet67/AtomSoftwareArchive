
0REM THE BEST OF INTERFACE PAGE 36-37
10 REM SPIRAL EATER (C) MAHOGANY 1982
20 PRINT $21;DIM P(-1);@=0
30 [;JSR #FE71;STY #80;RTS;]
40 Y=0;PRINT $6;GOSUB 310
50 Q=99999;?#E1=0;C=2;L=15;CLEAR 0;K=269
60 K?#8000=42;GOTO b
70a REM READ KEYBOARD
80 Z=32*L+C;LINK TOP
90 IF ?#80=58 THEN C=C-1;IF C<0 THEN C=0
100 IF ?#80=33 THEN L=L+1;IF L>15 THEN L=15
110 IF ?#80=44 THEN L=L-1;IF L<0 THEN L=0
120 IF ?#80=45 THEN C=C+1;IF C>31 THEN C=31
130 Z?#8000=64;IF L<15 THEN A=32*(L+1)+C
140 IF L>0 THEN B=32*(L-1)+C
150 IF C>0 THEN D=32*L+C-1
160 IF C<31 THEN E=32*L+C+1
170 IF (A?#8000=64)&(B?#8000=64)&(D?#8000=64)&(E?#8000=64);G.z
180 Z=32*L+C;IF Z?#8000=42 THEN GOTO h
190 Z?#8000=128;GOTO a
200z REM UPDATE SCORE
210 Q=Q-256;PRINT $30,Q;GOTO 180
220h PRINT"YOU DID IT!"''"SCORE IS "Q'
230 IF Q>Y THEN Y=Q
240 PRINT"HIGHEST SCORE IS "Y';LINK #FFE3;GOTO 50
250 END
260b REM................
270 MOVE 0,0;PLOT 1,0,47;PLOT 1,63,0;PLOT 1,0,-47;PLOT 1,-55,0
280 PLOT 1,0,39;PLOT 1,47,0;PLOT 1,0,-31;PLOT 1,-39,0
290 PLOT 1,0,24;PLOT 1,30,0;PLOT 1,0,-16;PLOT 1,-22,0
300 PLOT 1,0,8;PLOT 1,13,0;GOTO a
310 PRINT $12
320 PRINT"      ********************"'
330 PRINT"      *** SPIRAL EATER ***"'
340 PRINT"      ********************"''
350 PRINT"THIS GAME DRAWS ASPIRAL WHICH YOU MUST EAT BY "
360 PRINT"RUNNING OVER IT. YOU AIM TO END ON TOP OF THE "
370 PRINT"ASTERISK IN THE CENTRE OF THE SPIRAL. YOU LOSE "
380 PRINT"POINTS EVERY TIME YOU LEAVE THE SPIRAL."''
390 PRINT"PRESS ANY KEY TO CONTINUE";LINK #FFE3;PRINT $12
400 PRINT"CONTROLS:"''"Z - LEFT"'"M - RIGHT"'"L - UP"'
410 PRINT"A - DOWN"''"PRESS ANY KEY TO START";LINK #FFE3
420 PRINT $12;RETURN
