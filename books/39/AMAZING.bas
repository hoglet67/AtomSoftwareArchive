
0 REM THE BEST OF INTERFACE PAGE 25-26
10 REM AMAZING BY R. HEINS & R. HEARD
20 Z=#B002;M=0;N=0
30 GOSUB a
40 PRINT $21
50 DIM VV(4),NN(3),A(3),P(-1)
60[;:NN0 JSR#FE71;STY#2800;RTS;]
70[;:VV0 LDAZ;LDY#81;:VV1  LDX#80;:VV2 DEX;BNEVV2;EOR@4
80 STAZ;DEY;BNEVV1;RTS;];PRINT $6
90 ?#80=ABSRND%30+8
100bP.$12"WHICH LEVEL-1 TO 3 (3=HARD)";INPUT U
110 IF U<1 OR U>3 THEN GOTO b
120x PRINT $12;CLEAR 0
130 FOR I=1 TO U*115;A=ABSRND%(#1FF)+#8000;?A=255;NEXT I
140 A=#8021;B=#81DE
150 ?A=#23;?B=#2A;F=0
160 FOR V=#8020 TO #81C0 STEP 32;?V=#A8;NEXT V
170 FOR V=#81E1 TO #81FE;?V=#AB;NEXT V
180 FOR V=#81DF TO #803F STEP -32;?V=#A9;NEXT V
190 FOR V=#801E TO #8001 STEP -1;?V=#AB;NEXT V
200 ?#8000=#1B;?#81E0=#1B;?#801F=#1D;?#81FF=#1D
210 FOR L=0 TO 20
220 V=ABSRND%(#1FF)+#8000
230 IF ?V=#40 THEN ?V=#24
240 J=ABSRND%(#1FF)+#8000
250 IF ?J=#40 THEN ?J=#21
260 NEXT L
270fLINK NN0
280 O=B
290 IF ?#2800=32 THEN B=B-32
300 IF ?#2800=31 THEN B=B+32
310 IF ?#2800=27 THEN B=B-1
320 IF ?#2800=1 THEN B=B+1
330 IF ?B=#FF OR ?B=#AB OR ?B=#A9 OR ?B=#A8 THEN GOTO c
340 IF ?B=#23 THEN GOTO d
350 IF ?B=#21 THEN M=M+10
360 IF ?B=#24 THEN M=M+15
370 ?O=#40;?B=#2A
380 GOSUB e
390 GOTO f
400dP.$12"YOU MADE IT HOME-AT LAST !"'''
410 FOR O=0 TO 60;LINK VV0;NEXT O
420 PRINT"YOUR SCORE=",M'
430 GOTO z
440eFOR J=0 TO 20
450 G=ABSRND%(#1FF)+#8000
460 IF ?G=255 THEN ?G=#40
470 NEXT J
480 FOR J=0 TO 15
490 G=ABSRND%(#1FF)+#8000
500 IF ?G=#40 THEN ?G=255
510 NEXT J
520 RETURN
530cP.$12"BAD LUCK !"'"YOU HIT HE WALL !"'''
540 PRINT"YOU ARE DEAD !*?"''
550F.Q=0TO200;?Z=?Z:4;N.Q;P.$7;F.Q=0TO200;?Z=?Z:4;N.Q;P.$7
560 FOR Q=0 TO 400;?Z=?Z:4;NEXT Q
570 PRINT"NO SCORE"'
580zINPUT"ANOTHER GO (Y OR N) "$A
590 IF ?A=CH"Y" THEN GOTO x
600 PRINT $12;END
610aP.$12"      AMAZING!"''"YOU ARE AN ASTERISK !"'
620P."YOU MUST FIND YOUR WAY TO THE"'"TOP LEFT OF SCREEN-"
630 PRINT"(#)"''
640 PRINT"    '@'               UP"'
650 PRINT"';'     '['       LEFT  RIGHT"'
660 PRINT"    '/'              DOWN"'
670P.'"YOU MUST PICK UP AS MANY"'"POINTS AS POSSIBLE!"'
680P."$=15 PTS!; !=20 PTS. BUT BEWARE,THE MAZE IS CHANGING!!"'
690P.'"PRESS ANY KEY TO START";LINK#FFE3;RETURN
