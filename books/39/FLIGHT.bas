
0 REM THE BEST OF INTERFACE PAGE 26-27-28
10 REM FLIGHT BY G. BUTTRICK
20 GOSUB 1350
30S=0;W=0;E=0;M=0;N=0;@=3;Z=0;L=0;C=90;H=0
40D=15;V=230;G=900;X=32;Y=25;F=500;B=#DE
50 DIMP(-1);P.$21;[;JSR#FE71;STY#80;RTS;];P.$6
60 CLEAR0;?#E1=0
70MOVE32,30;PLOT1,0,-10;LI.T.;R=?#80;IFR=52 PLOT15,X,Y;X=32
80 C=90
90IFA.R.%4=0 PLOT15,X,Y;X=X-4;F.K=1TO10;WAIT;N.;PLOT13,X,Y
100IFA.R.%4=1 PLOT15,X,Y;X=X+4;F.K=1TO10;WAIT;N.;PLOT13,X,Y
110A=R.%3;IFR=40 AND D<3 N=1
120IFD<3 AND N=1 A=0
130 IFR=54 V=V+10;F=F-10
140 IFR=35 V=V-10
150 V=V+5;IFABS(X-32)>8 L=L+1
160 IFV<205 V=V-7
170 IFV>350 V=350
180MOVE32,44;PLOT1,-10,-A;MOVE32,44;PLOT1,10,A;F.K=1TO60;WAIT
190 N.;MOVE32,44;PLOT3,-10,-A;MOVE32,44;PLOT3,10,A
200E=E+1;IFE%5=0 D=D-1
210 IFD<0 D=0
220 F=F-5;G=G-2700/V;IFG<1 G=0
230 !B=#198080;P."HEADING"
240 C=C-(32-X)/4;IFC>93 C=93
250 IFC<87 C=87
260IFX=32 C=90
270!B=#1B80A0;P.C
280IFD=14 AND R=44 H=1
290!B=#1B80A0;IFH=1 P."   "
300IFH=1 C=C-90;IFC<0 C=C+360
310!B=#1B80A0;IFH=1 P.C
320IFD=7ANDR=44 H=2
330!B=#1B80A0;IFH=2 P."   "
340IFH=2 C=C+180
350!B=#1B80A0;IFH=2 P.C
360IFD<14ANDH=0 G.m
370IFD<6 ANDH<>2 G.n
380!B=#1581C0;P."DRIFT";!B=#1581E0;P.X-32
390!B=#018000;P."TRIM";!B=#018020;IFN=1AND D<3 P."CORRECT"
400!B=#0D8040;P."HORIZON"
410!B=#198150;IFD<5;P."final approach"
420!B=#81C0;P."A/S";!B=#81E0;P.V
430!B=#178190;P."MILES";!B=#0781C0;P."TO T/D";!B=#0881E0;P.D
440!B=#1081C0;P."ALT";!B=#1081E0;P.G
450!B=#8060;IFV<185;P."stall!!disaster!!";P.$7$7$7$7$7;G.a
460IFV<205 ANDV>=185 P.$7$7"A/S TOO LOW"
470 IFV>=205 P."           "
480!B=#8080;IFX>36P.$7"TO RIGHT OF TRACK"
490IFX<28 P.$7"TO LEFT OF TRACK"
500 IFX>24 AND X<36 P."                 "
510!B=#1D8000;P."U/C";!B=#1D8020;IFM=0P."UP"
520!B=#1C8020;IFR=53 M=1;IFM>=1ANDD<10 P."DOWN";M=M+1
530!B=#1A8040;F.J=1TO30;WAIT;N.;IFM>1 AND D<10 P."LOCKED"
540!B=#1C81C0;P."FUEL";!B=#1C81E0;P.F
550  !B=#1B8100;IFD<10 P."OUTER"
560!B=#1A8120;IFD<10P."BEACON"
570!B=#018100;IFD<3 P."INNER"
580!B=018120;IFD<3 P."BEACON"
590!B=#1B8160;IFD<13 P."TOWER"
600!B=#138180;IFR=55 AND D<13 AND D>10 W=1
610 IF W=1 P."CLEAR TO LAND"
620IF F<1 F=0;P.$27$12"         out of fuel!!";F.J=1TO120;WAIT
630 IF F<1 N.;G.a
640IFD=0ANDG<100ANDM>=1ANDN=1ANDX<40ANDX>24 S=1
650IFS=1 ANDV<=255 ANDV>=205 G.b
660IFD=0 AND G>=100 G.g
670IFD=0 AND M=0 P.$7$7$27$12"         no wheels!!";F.J=1TO60
680IF D=0 AND M=0 WAIT;N.;G.a
690IF D=0 AND V>255 G.c
700IFD=0ANDG<100ANDM>=1ANDN=1AND(X<=44ORX>=20) Z=1
710IFZ=1ANDV<=255ANDV>=205 G.d
720IFD=0ANDG<100ANDM>=1AND N=0 G.e
730 IFD<3 AND W=0 G.h
740 IFD=0 AND V<205 G.j
750IF ABS(X-32)>12 AND D<5 F.J=1TO7;P.$7;N.;G.f
760 G.70
770 E.
780aF.J=1TO200;?#B000=#F0;WAIT;?#B000=0;WAIT;N.;CLEAR4
790F.J=1TO300;WAIT;N.;P.$27$12"          nasty!!"
800GOS.l;P.$12;E.
810b CLEAR4;MOVE125,192;PLOT5,118,170;MOVE131,192
820 PLOT5,138,170;F.J=1TO30;WAIT;N.
830MOVE125,192;PLOT7,118,170;MOVE131,192;PLOT7,138,170
840 MOVE120,192;PLOT5,110,160;MOVE136,192;PLOT5,146,160
850F.J=1TO30;WAIT;N.;MOVE120,192;PLOT7,110,160;MOVE136,192
860 PLOT7,146,160
870MOVE115,192;PLOT5,100,150;MOVE141,192;PLOT5,156,150
880F.J=1TO30;WAIT;N.;MOVE115,192;PLOT7,100,150;MOVE141,192
890PLOT7,156,150
900MOVE100,192;PLOT5,80,130;MOVE156,192;PLOT5,176,130
910F.J=1TO30;WAIT;N.;MOVE100,192;PLOT7,80,130;MOVE156,192
920PLOT7,176,130
930MOVE90,192;PLOT5,40,0;MOVE166,192;PLOT5,216,0
940 GOS.l
950 P.$27$12"   WELL DONE! SAFE LANDING";GOS.l
960P.''"PILOT RATING"(280+F-4*L)/4-4"%";E.
970cP.$7$7$27$12"A/S TOO HIGH!! overshoot!"''
980P."GO ROUND AGAIN";GOS.l;G.30
990dP.$7$7$27$12"WELL YOU'RE DOWN,SOMEHOW!"''
1000P."BUT YOU SHOOK YOUR PASSENGERS!"''
1010P."YOU ARE"ABS(32-X)" YARDS OFF THE RUNWAY!";E.
1020eP.$7$7$27$12"YOU LANDED ON A WING AND A"'"PRAYER!"''
1030P."GET YOUR HORIZON RIGHT NEXT TIME!"''
1040P."(IF THERE IS ONE)"''
1050P."YOU HAVE 8 SECONDS TO CLEAR THE PLANE"
1060 F.J=1 TO 480;WAIT;N.;G.a
1070f P.$7$7$27$12"        emergency!!!"''
1080P."ONLY 4 MILES TO T/D-YOU ARE TOO"'"FAR OFF TRACK!!"''
1090P."AND ON COLLISION COURSE WITH"'"ANOTHER AIRCRAFT!"''
1100P."press emergency button-e!"''
1110F.J=1TO120;WAIT;N.
1120LINK TOP;R=?#80;IFR=37 GOS.z;G.30
1130P."too late!!!";GOS.l;G.a
1140gP.$7$7$7$27$12"too high!toohigh!!"''
1150P."YOU ARE HEADING STRAIGHT FOR"'"THE TOWER!"''
1160GOS.l;IFA.R.%3=0 GOS.z;G.30
1170 G.a
1180h F.J=1TO7;P.$7;N.;P.$27$12
1190P."    no contact with tower!"''
1200P."RUNWAY BLOCKED!!";GOS.l;G.a
1210j P.$7$7$27$12"you'll never make it!!A/S TOO LOW!!";GOS.l
1220 G.a
1230m F.J=1TO10;P.$7;N.;P.$27$12
1240P."you are way off course!!"''
1250P."YOU FAILED TO TURN LEFT"''"ON TO COURSE 000"''
1260P."AT 14 M. TO T/D"''
1270P."RE-JOIN STACK!!";GOS.l;GOS.l;G.30
1280nF.J=1TO10;P.$7;N.;P.$27$12
1290P."shocking error!!"''"YOU FAILED TO TURN LEFT"''
1300P."ON TO FINAL APPROACH-COURSE 270"''
1310P."AT 7 M. FROM T/D"''"tower block ahead!!!";GOS.l
1320 P.''"look out!!";GOS.l;GOS.l;G.a
1330zP."lucky escape!!"''"APPROACH AGAIN";GOS.l;GOS.l;WAIT;R.
1340l F.J=1 TO180;WAIT;N.;R.
1350 P.$12
1360 P."         **************"'
1370 P."         *** flight ***"'
1380 P."         **************"''
1390 P."YOU ARE THE CAPTAIN OF A JET"'
1400 P."TRYING TO LAND ON HEATHROW"'
1410 P."IN HEAVY FOG."'"YOU HAVE TO RELY ENTIRELY ON"'"YOUR " 
1420 P."INSTRUMENTS"''"CONTROL KEYS:"'
1430 P."C E H L T U V W"''"PRESS ANY KEY TO START"'
1440 LINK #FFE3;P.$12;RETURN
