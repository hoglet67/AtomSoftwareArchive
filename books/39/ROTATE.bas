
0 REM THE BEST OF INTERFACE PAGE 15-16
10 REM NUMBER ROTATE BY MARKUS JAKOBSSON
20 DIM RR(3),W(11),F(0),A(10),P(-1);R=#FFE3;$W="@123456789YN"
30 PRINT $21;[;:RR1 STX F;RTS;:RR0 JSR R;LDX @LEN(W)-1
40:RR2 CMP W,X;BEQ RR1;DEX;BPL RR2;BMI RR0;];PRINT $6,$12
50 PRINT"NUMBER ROTATE"'';?#E1=0;PRINT"INSTRUCTIONS (Y/N)?"'
60 LINK RR0;IF ?F<9;P."IMPOSSIBLE NOT MORE THAN 9!"';GOTO 60
70 IF ?F=10 THEN PRINT $12;GOSUB a
80 PRINT $12"OK! LETS GO!"';?#E1=0;@=2
90 FOR K=1 TO 9;A?K=ABSRND%9+1;NEXT K;O=0
100j Q=0;FOR J=1 TO 8;FOR K=(J+1) TO 9
110 IF A?J=A?K THEN A?K=ABSRND%9+1;Q=1
120 NEXT K;NEXT J;IF Q=1 THEN GOTO j
130 T=0;GOSUB p
140 ?#E1=0;PRINT"HOW MANY DO YOU WANT TO REVERSE?"
150i LINK RR0;PRINT';IF ?F=0 THEN GOTO 210
160 IF ?F>9 THEN PRINT"WRONG INPUT !"';GOTO i
170 T=T+1;FOR K=1 TO (?F/2);Z=A?K;A?K=A?(?F-K+1)
180 A?(?F-K+1)=Z;NEXT K;GOSUB p;Q=0;FOR K=1 TO 8
190 IF A?K<>K THEN Q=1
200 NEXT K;IF Q=1 THEN GOTO 140
210 @=0;P."YOU DID IT IN "T" MOVES!"'''"ONE MORE GAME(Y/N)?"'
220 LINK RR0;IF ?F=10 THEN PRINT $12;GOTO 90
230 IF ?F=11;P.$12"THANKS FOR THE GAME(S)!";END
240 PRINT"WRONG INPUT!"';GOTO 220
250p O=O+1;IF O=5 THEN O=0;PRINT $12
260 @=2;P.';FOR K=1 TO 9;PRINT A?K;NEXT K;PRINT'';RETURN
270a PRINT'"NINE NUMBERS WILL APPEARE IN"'"DISORDER ON THE "
280 PRINT"SCREEN!"'"YOU SHALL ARRANGE THEM IN"'"NUMERICAL "
290 PRINT"ORDER FROM LEFT"'"TO RIGHT!"'"YOU JUST TELL HOW MANY"
300 PRINT" NUMBERS"'"FROM THE LEFT YOU WANT TO"'"REVERSE! IF "
310 PRINT"THE LIST IS:"'"  4 3 2 1 5 6 7 8 9"'
320 PRINT"YOU REVERSE THE FOUR NUMBERS"'"4 3 2 1, WHEN YOU "
330 PRINT"PRESS 4 AND"'"THE NEW LIST WILL BE THIS ONE:"'
340 PRINT"  1 2 3 4 5 6 7 8 9"'"PRESSING @ GIVE YOU AN ESCAPE!"
350 PRINT''"PRESS ANY KEY WHEN READY!";LINK #FFE3;RETURN
