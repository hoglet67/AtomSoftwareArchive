
0 REM THE BEST OF INTERFACE PAGE 24
10 REM HUMBLE STRUMMER BY AMALIA BRIDGE
20 GOSUB 220
30 PRINT $12,$21;Y=RND
40 DIM VV(4),P(-1);L=#B002
50[;:VV0 LDA L;LDY #81;:VV1 LDX #80;:VV2 DEX;BNE VV2
60 EOR @4;STA L;DEY;BNE VV1;RTS;]
70 PRINT $6;?#E1=0;@=0
80 A=ABSRND%100+1
90 FOR J=1 TO 10;PRINT"THIS IS GUESS "J'
100 INPUT"WHAT NUMBER AM I THINKING OF"B
110 FOR Z=1 TO 100-ABS(A-B)
120 ?#80=ABS(A-B);LINK VV0
130 NEXT Z
140 IF B=A THEN GOTO 170
150 NEXT J
160 GOTO 180
170 PRINT"YES, YOU GUESSED IT"'
180 PRINT"I WAS THINKING OF "A'
190 PRINT"ENTER Y FOR ANOTHER GO"'
200 INPUT C;IF C=Y;GOTO 80
210 ?#E1=128;END
220 PRINT $12
230 PRINT"    ************************"'
240 PRINT"    *** THUMBLE STRUMMER ***"'
250 PRINT"    ************************"''
260 PRINT"THE COMPUTER THINKS OF A NUMBER BETWEEN 1 AND 100, "
270 PRINT"AND RESPONDS TO YOUR GUESS BY SOUNDING A MUSICAL "
280 PRINT"NOTE. THE HIGHER THE PITCH THE CLOSER TO THE CORREC"
290 PRINT"T NUMBER YOU ARE."''"PRESS ANY KEY TO START"
300 LINK #FFE3;RETURN
