
0 REM THE BEST OF INTERFACE PAGE 13
10 REM STARWARS BY G. HILLEBRAND
20 GOSUB 340
30 DIM P(-1);PRINT $21;[;JSR #FE71;STY #80;RTS;];PRINT $6
40 Q=6;A=10;B=160;E=1000;CLEAR 3;MOVE 64,76;DRAW 64,116
50 MOVE 44,96;DRAW 84,96;MOVE 59,76;DRAW 69,76;MOVE 59,116
60 DRAW 69,116;MOVE 44,91;DRAW 44,101;MOVE 84,91;DRAW 84,101
70 MOVE 62,86;DRAW 66,86;MOVE 62,106;DRAW 66,106;MOVE 54,94
80 DRAW 54,98;MOVE 74,94;DRAW 74,98;MOVE 34,66;DRAW 94,66
90 DRAW 180,0;MOVE 34,66;DRAW -52,0
100 LINK TOP
110 IF ?#80=49 THEN A=A-5
120 IF ?#80=55 THEN A=A+5
130 IF ?#80=48 THEN B=B+5
140 IF ?#80=32 THEN B=B-5
150 A=A+RND%5;B=B+RND%5;IF A>113 THEN A=ABS(A)-8
160 IF B>180 THEN B=ABS(B)-8
170 IF A<0 THEN A=ABS(A)+8
180 IF B<0 THEN B=ABS(B)+8
190 IF ?#80=0 THEN GOSUB 240
200 GOSUB 210;WAIT;GOSUB 210;GOTO 100
210 MOVE A,B;PLOT Q,A,(B+10);MOVE A,(B+5);PLOT Q,(A+7),(B+10)
220 PLOT Q,(A+14),(B+5);PLOT Q,(A+7),B;PLOT Q,A,(B+5)
230 MOVE (A+14),B;PLOT Q,(A+14),(B+10);RETURN
240 MOVE 34,66;PLOT 6,64,96;FOR W=1 TO 3;WAIT;NEXT W
250 MOVE 34,66;PLOT 6,64,96
260 IF ABS(A-64)<6 AND ABS(B-96)<6 THEN GOTO 280
270 E=E-10;RETURN
280 CLEAR 0;FOR W=0 TO 100;?#B000=#B0;?#B000=0;?#B002=?#B002:4
290 NEXT W;PRINT"YOU'VE GOT HIM!!!"'''''"YOUR SCORE:"E'
300 PRINT"ANOTHER GAME (Y OR N)";DO LINK TOP
310 UNTIL ?#80=57 OR ?#80=46
320 IF ?#80=57 THEN GOTO 40
330 PRINT $12'''''''"         SEE YOU AROUND"''';END
340 PRINT $12
350 PRINT"       *****************"'
360 PRINT"       *** star wars ***"'
370 PRINT"       *****************"''
380 PRINT"IN THIS GAME YOU HAVE TO MOVE THE X-WING FIGHTER IN"
390 PRINT"TO LINE WITH YOUR SIGHTS, AND SHOOT IT DOWN WITH A "
400 PRINT"LASER BEAM."'''"PRESS ANY KEY TO CONTINUE"
410 LINK #FFE3;PRINT $12"CONTROLS:"''
420 PRINT"P - UP"'"@ - DOWN"'"Q - LEFT"'"W - RIGHT"'"SPACE - "
430 PRINT"FIRE"''"PRESS ANY KEY TO START"';LINK #FFE3;RETURN
