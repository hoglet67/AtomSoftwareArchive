
0 REM THE BEST OF INTERFACE PAGE 17-18
10 REM REVERSI BY J. RUSTON
20 GOSUB 430;GOSUB 270
30 INPUT"YOUR MOVE"A;PRINT $11;LINK #FE22;J=0
40 IF A=99 THEN J=1;GOTO 110
50 A=A%10+(A/10)*32+#8001;S=A;GOSUB 310
60 IF C<>3 THEN GOTO 30
70 ?A=8;Q=0;FOR T=1 TO 8;S=A;DO S=S+AA(T);GOSUB 310;UNTIL C<>2
80 IF (S-A)=AA(T) OR ?S<>8 THEN GOTO 100
90 FOR G=A TO S STEP AA(T);?G=8;NEXT G;Q=Q+1
100 NEXT T;IF Q=0 THEN ?A=46;GOTO 30
110 PRINT"PRESS RETURN FOR MY MOVE"';LINK #FFE3;PRINT $11
120 LINK #FE22;M=0;H=0;P=0;FOR B=11 TO 88
130 D=B%10+(B/10)*32+#8001;S=D;X=?D;GOSUB 310
140 IF C<>3 THEN GOTO 210
150 X=?D;?D=32;Q=0;FOR T=1 TO 8;G=D;K=-1;DO G=G+AA(T);S=G
160 GOSUB 310;K=K+1;UNTIL C<>1;IF C<>2 THEN K=0
170 Q=Q+K;NEXT T
180 IF B=11 OR B=18 OR B=81 OR B=88 THEN Q=Q*10;GOTO 200
190 T=B/10;R=B%10;IF T<>2;IF T<>7;IF R<>2;IF R<>7;Q=Q*2
200 IF Q>H THEN H=Q;?M=46;M=D;X=174
210 ?D=X;NEXT;IF M=0 AND J=1 THEN GOTO 350
220 FOR P=1 TO 8;S=M;DO S=S+AA(P);GOSUB 310;UNTIL C<>1
230 IF (S-M)=AA(P) OR C<>2 THEN GOTO 250
240 S=M;DO ?S=3;S=S+AA(P);GOSUB 310;UNTIL C<>1
250 NEXT P;GOTO 30
260 GOTO 540
270 @=0;DIM AA(8);AA(1)=-33;AA(2)=-32;AA(3)=-31;AA(4)=-1
280 AA(5)=1;AA(6)=31;AA(7)=32;AA(9)=33;CLEAR 0;PRINT $30
290 PRINT"  12345678"';FOR T=1 TO 8;PRINT" "T"........ "'
300 NEXT T;?#8085=8;?#8086=3;?#80A5=8;?#80A6=3;RETURN
310 C=0;IF ?S=8 THEN C=1
320 IF ?S=3 THEN C=2
330 IF ?S=46 OR ?S=174 THEN C=3
340 RETURN
350 H=0;C=0;FOR E=11 TO 88;S=E%10+(E/10)*32+#8001
360 IF ?S=8 THEN H=H+1
370 IF ?S=3 THEN C=C+1
380 NEXT E
390 PRINT"YOU GOT "H"; I GOT "C'
400 IF C=H THEN PRINT"DRAW";GOTO 540
410 IF C>H THEN PRINT"I WIN ";GOTO 540
420 IF C<H THEN PRINT"YOU WIN";GOTO 540
430 PRINT $12
440 PRINT"        ***************"'
450 PRINT"        *** REVERSI ***"'
460 PRINT"        ***************"''
470 PRINT"ALL MOVES ARE ENTERED AS A TWO DIGIT NUMBER, THE "
480 PRINT"FIRST DIGIT BEING THE DISPLACEMENT DOWN THE BOARD, "
490 PRINT"AND THE SECND THE DISPLACEMENT ACROSS IT."'
500 PRINT"THE PROGRAM INVERTS ANY POSITION IT IS CONSIDERING "
510 PRINT"MOVING TO, SO YOU CAN 'SEE IT THINKING'."'"IF YOU "
520 PRINT"DON'T WANT TO, OR CAN'T MOVE, TYPE 99."''
530 PRINT"PRESS ANY KEY TO START";LINK #FFE3;RETURN
540 INPUT''"ANOTHER GAME (Y/N) "$Z
550 IF ?Z=CH"Y" THEN GOSUB 270;GOTO 30
560 PRINT $12'''''''"THANK YOU AND GOODBY"''';END
