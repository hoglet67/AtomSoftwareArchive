
0 REM THE BEST OF INTERFACE PAGE 6
10 GOSUB 270
20 CLEAR 0;A=0;?#E=0
30 FOR W=#8080 TO #80BF;?W=255;NEXT W
40 FOR W=#80C0 TO #80FF;?W=127;NEXT W
50 DIM V(5);!V=#04081020;V!4=#102;DIM NN(5),P(-1);PRINT $21;[
60:NN0 JSR#FE71;STY#80;RTS;];PRINT $6
70 FOR W=#8020 TO #803F;?W=#70;NEXT W
80 FOR W=#8040 TO #81E0 STEP #20;?W=#6A;NEXT W
90 FOR W=#805F TO #81FF STEP #20;?W=#55;NEXT W
100 ?#8020=#7A;?#803F=#75;O=5
110 X=ABSRND%58+2;Y=ABSRND%20+1;U=6;W=0;K=1;L=1
120 PLOT 13,U,W;PLOT 1,2,0
130 PLOT 15,X,Y;X=X-1+(L=1)*2;Y=Y-1+(K=1)*2
140 IF X<2 OR X>61 L=ABS(L-1)
150 IF Y>42 K=ABS(K-1)
160 IF Y<0 O=O-1;P.$30$10$8$8$8$8$8$8$8$8$8$8,O;IF O<>0 GOTO110
170 IF O=0 GOTO 390
180 GOSUB 250;IF Q=0 GOTO 210
190 PLOT 15,X,Y;PLOT 11,1,0;A=A+10;K=ABS(K-1);PRINT $30,A
200 FOR C=0 TO 10;?#B002=?#B002:4;NEXT C
210 PLOT 13,X,Y;LINK NN0;IF ?#80=255 GOTO 240
220 IF ?#80=58 U=U-2
230 IF ?#80=31 U=U+2
240 MOVE 1,0;PLOT 7,62,0;PLOT 13,U,W;PLOT 1,2,0;GOTO 130
250 P=X/2+(47-Y)/3*32+#8000
260 Q=(?P&(V?(X&1+(47-Y)%3*2))<>0);RETURN
270 PRINT $12
280 PRINT"        ****************"'
290 PRINT"        *** BREAKOUT ***"'
300 PRINT"        ****************"''
310 PRINT"THE RULES WILL BE KNOWN,"'"I PRESUME."''
320 PRINT"CONTROLS:"'"Z = LEFT"'"? = RIGHT"''
330 PRINT"PRESS KEY TO CONTINUE";LINK #FFE3;PRINT $12
340 PRINT"YOU WILL BE ABLE TO SEE YOUR SCORE IN THE TOP LEFT "
350 PRINT"HAND OF THE SCREEN, AND THE NUMBER OF 'BALLS TO GO'"
360 PRINT" IS SHOWN IN THE TOP RIGHT."'
370 PRINT"YOU START THE GAME WITH 5 BALLS."''
380 PRINT"PRESS KEY TO START";LINK #FFE3;PRINT $12;RETURN
390 INPUT'"ANOTHER GAME (Y OR N)"$B
400 IF ?B=CH"Y" GOTO 20
410 IF ?B=CH"N" PRINT $12''''''"      SEE YOU AGAIN"'''';END
420 GOTO 390
