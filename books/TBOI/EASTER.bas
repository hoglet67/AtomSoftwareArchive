
0 REM THE BEST OF INTERFACE PAGE 34-35-36
10 REM SEARCHING FOR EASTER SUNDAY
20 REM TWO DIFFERENT APPROACHES ARE POSSIBLE, YOU CAN GET A LIST
30 REM OF THE DATES OF EASTER SUNDAY STARTING A GIVEN YEAR OR
40 REM YOU CAN ASK FOR A SPECIFIED DAY - THE ATOM WILL SEARCH
50 REM AND FIND WHEN EASTER SUNDAY WILL BE ON THIS DAY!
60 REM *********************************************************
70 DIM P(-1)
80 PRINT $21
90[ JSR#FFE3;STA #80;RTS;]
100 PRINT $6
110 PRINT $12"   SEARCHING FOR EASTER SUNDAY."''
120 PRINT"THIS PROGRAM CALCULATES THE DATE OF EASTER SUNDAY."'
130 PRINT"YOU CAN GET A LIST (L) OR YOU"'
140 PRINT"CAN SEARCH FOR A GIVEN DATE (D)"''
150 PRINT"WHICH ALTERNATIVE (L/D)?"'
160 LINK TOP
170 IF ?#80=#4C THEN GOTO l
180 IF ?#80=#44 THEN GOTO d
190 PRINT"O NO! PLEASE TRY AGAIN"';GOTO 150
200l REM LIST
210 PRINT $12
220 INPUT"YEAR TO START"X
230 IF X>1583 AND X<4000 THEN GOTO 250
240 PRINT"ONLY BETWEEN 1583 AND 4000"';GOTO 220
250 Z=12+X
260 PRINT $12"    YEAR      EASTER SUNDAY"''
270 ?#E1=0
280 DO GOSUB i;@=8;PRINT X,"        ";@=3
290 IF N=4 THEN PRINT"APRIL"P" "
300 IF N=3 THEN PRINT"MARCH"P" "
310 IF P=1 OR P=21 OR P=31 THEN PRINT"ST"';GOTO 350
320 IF P=2 OR P=22 THEN PRINT"ND"';GOTO 350
330 IF P=3 OR P=23 THEN PRINT"RD"';GOTO 350
340 PRINT"TH"'
350 X=X+1;UNTIL X>=Z
360 PRINT'"CONTINUE THE LIST? (Y) ?"
370 LINK TOP
380 IF ?#80=#59 THEN Z=X+12;GOTO 260
390 GOTO 840
400i A=X-19*(X/19);B=X/100;C=X-100*B
410 D=B/4;E=B-4*D;G=(8*B+13)/25
420 F=19*A+B-D-G+15;H=F-30*(F/30)
430 M=(A+11*H)/319
440 I=C/4;K=C-4*I
450 Q=2*E+2*I-K-H+M+32;L=Q-7*(Q/7)
460 R=H-M+L+90;N=R/25
470 P=H-M+L+N+19;P=P-32*(P/32)
480 RETURN
490 GOTO 900
500d REM GIVEN DATE
510 PRINT $12"TELL ME WHICH DATE I SHALL TEST"'
520 PRINT"AS WELL AS THE YEAR WHICH I AM"'
530 PRINT"GOING TO START AT!"'
540 PRINT"MARCH (M) OR APRIL (A)"'
550 ?#80=0;LINK TOP
560 IF ?#80=#4D THEN Y=3;GOTO 590
570 IF ?#80=#41 THEN Y=4;GOTO 590
580 PRINT"JUST MARCH AND APRIL !"';GOTO 540
590 INPUT"WHICH DATE"Z
600 IF Y=3 AND Z<22 THEN PRINT"NOT BEFORE MARCH 22ND!"';G.540
610 IF Y=4 AND Z>25;PRINT"NOT LATER THAN APRIL 25TH"';G.540
620 IF Z<1 OR Z>31;PRINT"I CAN'T BELEIVE THAT!"';GOTO 590
630 INPUT"WHAT YEAR SHALL I START"X
640 @=0
650 IF X<1583 OR X>3999;PRINT"ONLY THE YEARS 1583-3999"7;G.630
660 PRINT $12"FIRST YEAR "X'
670u PRINT"EASTER SUNDAY ";IF Y=3 THEN PRINT"MARCH "Z
680 IF Y=4 THEN PRINT"APRIL "Z
690 IF Z=1 OR Z=31 THEN PRINT"ST";GOTO 730
700 IF Z=2 OR Z=22 THEN PRINT"ND";GOTO 730
710 IF Z=3 OR Z=23 THEN PRINT"RD";GOTO 730
720 PRINT"TH"
730 DO GOSUB i
740 X=X+1
750 IF X>4000 THEN GOTO o
760 UNTIL N=Y AND P=Z
770 PRINT" IN THE YEAR OF "X-1'
780 PRINT"CONTINUE WITH THIS DATE (Y)?"''
790 LINK TOP
800 IF ?#80=59 THEN GOTO u
810 PRINT"HOW ABOUT ANOTHER DAY (Y)?"'
820 LINK TOP
830 IF ?#80=#59 THEN GOTO d
840 PRINT $12$7"THANK YOU FOR YOUR INTEREST!";GOTO 900
850o REM IMPOSSIBLE
860 PRINT'
870 PRINT"DOES NOT OCCUR BEFORE THE YEAR OF 3999 - A'M SORRY!"'
880 PRINT''"IF YOU WANT TO TEST ANOTHER DAY"'"PRESS ANY KEY!"
890 LINK #FFE3;GOTO d
900 END
910 REM ******************************************************
920 REM A PROGRAM WRITTEN BY LARS JAKOBSSON, ALSTORP 5:57
930 REM S-24021 LODDEKOPENINGE, SWEDEN - 1981-1019
