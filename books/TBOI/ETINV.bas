
0 REM THE BEST OF INTERFACE PAGE 20
10 REM E-T INVADERS BY G. MATTHEWS
20 GOSUB 480;GOSUB 410
30 PRINT $12;?#E1=0;;FOR A=0 TO 3;?(#804D+A*2)=163;NEXT A
40 FOR A=0 TO 10;Y?(A*2)=35;Y?(64+A*2)=35;Y?(128+A*2)=35
50 Y?(192+A*2)=35;NEXT A;L?-2=60;L?-1=29;?L=15;L?1=27;L?2=62
60 B=#81F0;GOSUB 360;GOSUB 120
70 GOSUB 320;GOSUB 90;IF ?#80=19 THEN GOTO 150
80 WAIT;GOSUB 90;WAIT;WAIT;GOTO 70
90 LINK #81;IF ?#80=17;IF B>#81E5;B=B-1;GOTO 120
100 IF ?#80=18;IF B<#81FA;B=B+1;GOTO 120
110 RETURN
120 B?-2=32;B?-1=65;?B=158;B?1=66;B?2=32
130 IF M>B-2;IF M<B+2 THEN GOTO 430
140 RETURN
150 N=B+64
160 N=N-96;IF ?N<>32 THEN GOTO 210
170 IF N?-32<>32 THEN N=N-32;GOTO 210
180 IF N?-64<>32 THEN N=N-64;GOTO 210
190 IF N<#8060 THEN GOTO 70
200 ?N=30;FOR A=1 TO 18;NEXT A;?N=32;GOTO 160
210 P=32;IF ?N=35 THEN G=8;S=S+4;X=X-1;GOTO 280
220 IF ?N=42 THEN G=6;S=S+3;P=Z;GOSUB 360;GOTO 280
230 IF N=L THEN G=20;S=S+10;L!-2=#20202020;L?2=32;GOTO 280
240 IF ?N=60 OR ?N=62 THEN G=2;GOTO 280
250 IF ?N=29 OR ?N=27 THEN G=4;S=S+2;GOTO 280
260 IF ?N=163 THEN G=10;S=S+6;GOTO 280
270 GOTO 70
280 FOR A=1 TO G;?#B002=?#B002:4;NEXT A;IF X=0 THEN GOTO 300
290 ?N=P;PRINT $30"SCORE:"S;GOTO 70
300 Y=Y+32;IF Y>#80C6 THEN Y=#80C6
310 X=44;GOTO 30
320 ?M=Z;M=M+32+RND%2;IF M>#8200 THEN GOSUB 360;GOTO 320
330 IF M>L+96 THEN M=M+RND%3
340 IF M>B-2;IF M<B+2 THEN GOTO 430
350 Z=?M;?M=42;RETURN
360 M=B;IF M<#81E6 THEN M=#81E6
370 IF M>#81FA THEN M=#81FA
380 IF M%2 THEN M=M-1
390 DO M=M-32;UNTIL ?M=35 OR M<Y;IF M<Y THEN M=L
400 M=M+RND%Z;Z=?M;RETURN
410 X=44;Y=#8046;L=#8030;PRINT$21;P=#81;[;JSR#FE71;STY#80
420 RTS;];PRINT $6;S=0;@=0;RETURN
430 PRINT''''''''''''''''"YOUR SCORE WAS "S''
440 PRINT"ANOTHER GAME (Y/N)"';DO LINK #81
450 UNTIL ?#80=57 OR ?#80=46;IF ?#80=57 THEN GOTO 470
460 PRINT $12''''''"THANK YOU FOR THE GAME(S)"''';END
470 GOSUB 410;GOTO 30
480 PRINT $12
490 PRINT"     ********************"'
500 PRINT"     *** E-T INVADERS ***"'
510 PRINT"     ********************"''
520 PRINT"CONTROLS:"'"1 - LEFT"'"2 - RIGHT"'"3 - FIRE"''
530 PRINT"PRESS ANY KEY TO START";LINK #FFE3;RETURN
