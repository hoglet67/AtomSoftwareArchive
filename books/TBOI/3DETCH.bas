
0 REM THE BEST OF INTERFACE PAGE 29-30-31
10 REM THREEDEE ETCH
20 REM BY J RUSTON
30 GOSUB 470
40 GOSUB 250
50 GOSUB 290;GOSUB 110;CLEAR 4;GOSUB 430;IF P=1 GOTO 90
60 LINK E;A?#2800=?R;A=A+1;IF A=255 P.$12"FULL BUFFER";NEW
70 IF $R="E" THEN GOTO 40
80 GOSUB 160;GOSUB 130;DRAW C,D;GOTO 60
90 GOSUB 400;GOTO 50
100 REM ASSIGN VARIABLES
110 S=L*L+M*M;T=S+N*N;Q=SQR(T);H=SQR(S);RETURN
120 REM RETURNS PLOT CO'S
130 O=T-U*L-V*M-W*N;C=T*(V*L-U*M)*4/(H*O)+128
140 D=96+3+Q*(W*S-N*(U*L+V*M))/(H*O);RETURN
150 REM USER INPUT
160 IF $R="U" THEN W=W+G
170 IF $R="D" THEN W=W-G
180 IF $R="R" THEN U=U-G
190 IF $R="L" THEN U=U+G
200 IF $R="B" THEN V=V-G
210 IF $R="F" THEN V=V+G
220 IF $R="C" THEN ?(#27FE+A)=CH"E";A=0;CLEAR 4;GOSUB 400
230 RETURN
240 REM MACHINE CODE
250 DIM R(1),P(10);E=P;PRINT $21
260 [JSR#FFE3;STA R;RTS;]
270 R?1=13;PRINT $6;RETURN
280 REM USER INPUTS
290 PRINT $12;INPUT"SIZE "G,"X VIEWPOINT "L,"Y VIEWPOINT "M
300 INPUT"Z VIEWPOINT "N;A=0;PRINT"G - GET"'"P - PUT"'
310 PRINT"S - SAVE"'"L - LOAD"'"R - RANDOM?"'
320 LINK E;IF $R="G" THEN P=1;GOTO 380
330 IF $R="P" THEN P=0;GOTO 380
340 IF $R="S" THEN *SAVE"PICTURE" 2800 28FF
350 IF $R="L" THEN *LOAD"PICTURE"
360 IF $R="R" THEN GOTO 450
370 PRINT $7;GOTO 320
380 RETURN
390 REM AUTOMATIC PLOT
400 GOSUB 430;DO ?R=A?#2800;GOSUB 160;GOSUB 130;DRAW C,D
410 A=A+1;UNTIL A?#2800=CH"E" OR A=255;RETURN
420 REM INITIALIZE POINTER
430 W=0;U=0;V=0;GOSUB 130;MOVE C,D;RETURN
440 REM RANDOM
450 A=0;G=20;L=RND%100;M=RND%100;N=RND%100;CLEAR 4;GOSUB 400
460 FOR I=0 TO 60;WAIT;NEXT I;GOTO 450
470 PRINT $12"threedee etch"';F.N=1 TO 13;PRINT"*";N.N;P.''
480 PRINT"THIS PROGRAM ALLOWS YOU TO DRAW SHAPES OF YOUR "
490 PRINT"CHOISE, AND THEN MOVE THE VIEWPOINT FROM WHICH "
500 PRINT"YOU ARE LOOKING AT THEM"''
510 PRINT"PRESS ANY KEY TO CONTINUE";LINK #FFE3;PRINT $12
520 PRINT"CONTROLS:"''
530 PRINT"U - DRAW LINE UP"'"D - DRAW LINE DOWN"'
540 PRINT"R - DRAW LINE RIGHT"'"L - DRAW LINE LEFT"'
550 PRINT"B - DRAW LINE BACKWARDS"'"F - DRAW LINE FORWARDS"''
560 PRINT"PRESS ANY KEY TO START";LINK #FFE3;RETURN
