
0 REM THE BEST OF INTERFACE PAGE 8-9
10 GOTO 170
20 PRINT $12;G=0;DIM AA(99),LL(99),EE(8);EE(1)=11;EE(2)=10
30 EE(3)=9;EE(4)=1;EE(5)=-1;EE(6)=-9;EE(7)=-10;EE(8)=-11
40 C=144;Z=128;FOR B=1 TO 8;FOR D=1 TO 8;AA(B+10*D)=Z
50 IF ABSRND%5>2 THEN AA(B+10*D)=C
60 LL(B+10*D)=AA(B+10*D);NEXT D;NEXT B;PRINT $12
70r ?#E1=0;G=G+1;PRINT"GENERATION "G-1'''''
80 FOR U=1 TO 8;FOR B=1 TO 8;F=U+10*B
90 IF G=1 THEN GOTO 130
100 H=0;FOR T=1 TO 8;IF AA(F+EE(T))=C THEN H=H+1
110 NEXT T;IF ((H<2) OR (H>3)) THEN LL(F)=Z
120 IF H=3 THEN LL(F)=C
130 NEXT B;NEXT U
140 FOR M=11 TO 88;AA(M)=LL(M);NEXT M
150 FOR U=1 TO 8;PRINT"         ";FOR B=1 TO 8;F=U+10*B
160 PRINT $AA(F);NEXT B;PRINT';NEXT U;PRINT $30;GOTO r
170 PRINT $12
180 PRINT"          ************"'
190 PRINT"          *** life ***"'
200 PRINT"          ************"''
210 PRINT"THE GAME SIMULATES THE BIRTH,GROWTH AND DEATH OF A "
220 PRINT"CELL COLONY, PRODUCING FASCINATING EFFECTS AS IT "
230 PRINT"DOES SO. THE CELLS LIVE ON A GRID AND ARE BORN, "
240 PRINT"LIVE OR DIE ACCORDING TO CONWAYS RULES."'''
250 PRINT"PRESS ANY KEY TO CONTINUE";LINK #FFE3;PRINT $12
260 PRINT"CONWAYS RULES:"''
270 PRINT"1. EACH CELL ON THE GRID HAS EIGHT NEIGHBOURS."'
280 PRINT"2. EVERY CELL WITH TWO OR THREE NEIGHBOURS SURVIVES "
290 PRINT"TO THE NEXT GENERATION."'
300 PRINT"3. IF THERE ARE THREE, AND ONLY THREE, NEIGHBOURING "
310 PRINT"CELLS, ANEW CELL IS BORN."'
320 PRINT"4. ANY CELL WITH FOUR OR MORE NEIGHBOURS DIES FROM "
330 PRINT"OVERPOPULATION."''
340 PRINT"PRESS ANY KEY TO START";LINK #FFE3;GOTO 20
