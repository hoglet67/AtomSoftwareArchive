10 REM ... COMPILER ...
20 DIM SS(20),LL(20),II(30),JJ(30) 
30 DIM X(7),TT(20),RR(4)
35 RR0=#3A00;RR1=#50;RR2=#80;RR3=#94;RR4=#2900
40 F.N=0TO30;DIMI(6);IIN=I;JJN=RR0;N.
50 $II0="RDCH";JJ0=#FFE3;$II1="WRCH";JJ1=#FFF4
60 $II2="WRHEX";JJ2=#F802;$II3="SCREEN";JJ3=#8000
70 $II4="PORT";JJ4=#B000
115 F.N=0TO20;TTN=0;LLN=RR0;N.
210 P.$21;GOS.a
220 P.$6'"PRINT";IN.$100;GOS.a
228 P.'"SYMBOLS:"'
230 F.N=0TOI-1;P.&JJN,"  ",$IIN';N.
240 E.
900aG=0;A=RR4;I=5;P=RR0
910 S=0;R=0;H=0;T=0
920 GOS.s;GOS.m;R.
1000sREM STATEMENT
1010 GOS.b;GOS.x
1020 IF$X="IF"G.1400
1030 IF$X="BEGIN"G.1200
1040 IF$X="GOTO"G.1500
1045 IF$X="END"A=A-3;R.
1050 IF$X="PROC"G.1700
1060 IF$X="ARRAY"G.1800
1070 IF$X="RETURN"G.1900
1100 REM IDENT STATEMENT
1110 GOS.b
1120 IF?A=CH":"A=A+1;GOS.h;JJN=P;G.s
1130 IF?A=CH"("GOS.h;GOS.u;G.p
1135 GOS.j;IF?A=CH"["G.1300
1160 GOS.d;H=V;GOS.m;R.
1180dIF?A<>CH"=" P.$6"NO =";G.o
1190 A=A+1;GOS.e;GOS.v;L=V;GOS.l;GOS.v;R.
1200 REM BEGIN...END
1210 A=A-1;DO A=A+1
1220 GOS.s;GOS.b
1230 U. ?A<>CH";"
1240 GOS.x;IF$X="END"R.
1250 P.$6"NO END";G.o
1300 REM ARRAY=
1310 A=A+1;GOS.e;IF?A<>CH"]"P.$6"NO ]";G.o
1320 A=A+1;GOS.d;L=V;GOS.v
1325 IFL<=0[LDX @-L;STA V,X;];H=0;R.
1330 [LDX L;STA V,X;];H=0;R.
1400 REM IF...THEN...ELSE
1410 GOS.q;GOS.b
1420 GOS.x;IF$X="THEN"G.1430
1425 P.$6"NO THEN";G.o
1430 GOS.s;GOS.v;GOS.b
1440 GOS.x;IF$X="ELSE"G.1460
1445 A=A-N;[:LLV;];R.
1460 GOS.g;U=G;GOS.u;[JMP LLG;]
1470 [:LLV;];GOS.s
1490 GOS.v;[:LLV;];R.
1500 REM GOTO
1510 GOS.k;[JMP U;];R.
1700 REM PROC
1710 GOS.k;JJN=P;IF?A<>CH"("P.$6"MISSING BRACKET";G.o
1720 A=A+1;JJI=1;GOS.i;IFN=0G.1780
1730 U=N;GOS.u
1740 T=RR3;H=T;JJU=T;GOS.m
1745 IF?A<>CH")"P.$6"NO BRACKET";G.o
1750 A=A+1;GOS.b;IF?A<>CH";"P.$6"NO ;";G.o
1760 A=A+1;GOS.s
1770 GOS.v;N=V;GOS.v;JJN=V;[RTS;];R.
1780 IF?A<>CH")"P.$6"NO BRACKET";G.o
1782 A=A+1;GOS.b;IF?A<>CH";"P.$6"NO ;";G.o
1785 A=A+1;GOS.s;[RTS;];R.
1800 REM ARRAY
1810 A=A-1;DO A=A+1
1820 GOS.b;GOS.x;GOS.y
1830 IFN<>I P.$6"ARRAY DECLARED";G.o
1840 IF?A<>CH"["P.$6"BRACKET MISSING";G.o
1850 A=A+1;GOS.c;IFN=0P.$6"CONSTANT MISSING";G.o
1860 GOS.v;JJI=RR1+R;I=I+1;R=R-V+1
1870 IF?A<>CH"]"P.$6"BRACKET MISSING";G.o
1880 A=A+1;GOS.b;UNTIL?A<>CH",";R.
1900 REM RETURN
1910 GOS.e;GOS.v;L=V;GOS.l;H=0;R.
2000iREM IDENTIFIER
2010 GOS.x
2030jIF N=0 R.
2040 GOS.y
2050 IFN=I;I=I+1;R=R+1;JJN=R+RR1
2070 IFI>30P.$6"TOO MANY VARIABLES";G.o
2080 U=JJN;GOS.u;R.
2100cREM CONSTANT
2105 GOS.b
2110 N=-1;C=0;DO D=C;N=N+1;C=C*10
2120 C=C+A?N-CH"0"
2130 U.A?N<CH"0"ORA?N>CH"9"
2140 IFN=0 R.
2150 A=A+N
2160 U=-D;GOS.u;N=1;R.
2200kREM LABELS
2210 GOS.b;GOS.x
2220 IF N=0 P.$6"LABEL MISSING";G.o
2230hGOS.y
2250 IFN=I;I=I+1
2260 IFI>30P.$6"TOO MANY VARIABLES";G.o
2270 U=JJN;R.
2400yREM LOOKUP
2410 $III=$X;N=-1
2420 DO N=N+1;U.$IIN=$III;R.
3000eREM EXPRESSION
3010 GOS.b;GOS.f
3015 GOS.b
3020 IF?A=CH"+"OR?A=CH"-"OR?A=CH"&"OR?A=CH"|"O=?A;A=A+1;G.3035
3025 IF((?A=CH ">"A.A?1=CH">")OR(?A=CH"<"A.A?1=CH"<")):1 R.
3030 O=?A;A=A+2
3035 U=O;GOS.u
3040 GOS.f;GOS.v;U=V;GOS.v;O=V;GOS.v;L=V;GOS.l
3045 IF U<=0 G.3070
3050 IFO=CH"+"[CLC;ADC U;]
3055 IFO=CH"-"[SEC;SBC U;]
3060 IFO=CH"|"[ORA U;]
3065 IFO=CH"&"[AND U;]
3068 G.3190
3070 IFO=CH"+"[CLC;ADC @-U;]
3075 IFO=CH"-"[SEC;SBC @-U;]
3080 IFO=CH"|"[ORA @-U;]
3085 IFO=CH"&"[AND @-U;]
3160 IFO=CH"<"F.N=1TO-U;[ASL A;];N.
3180 IFO=CH">"F.N=1TO-U;[LSR A;];N.
3190 L=U;GOS.r;GOS.t
3195 G.3015
3200pREM PROC BODY
3210 IFA?1=CH")"A=A+2;G.3230
3220 GOS.f;GOS.v;L=V;GOS.l
3230 GOS.v;[JSR V;];H=0;R.
3600fREM FACTOR
3610 GOS.x;IFN=0G.3630
3615 IF?A=CH"("G.3690
3620 GOS.j;IF?A=CH"["G.3670
3625 R.
3630 GOS.c;IF N R.
3635 IF?A<>CH"(" P.$6"BRACKET MISSING";G.o
3640 A=A+1;GOS.e;GOS.b
3650 IF?A<>CH")" P.$6"BRACKET MISSING";G.o
3660 A=A+1;R.
3670 REM ARRAYS
3675 A=A+1;GOS.e;IF?A<>CH"]"P.$6"NO BRACKET";G.o
3680 A=A+1;GOS.v;L=V;GOS.l;GOS.v
3685 [TAX;LDA V,X;];G.t
3690 GOS.h;GOS.u;GOS.p;G.t
4000qREM LOGICAL
4010 GOS.b;GOS.e;GOS.b
4020 U=0
4030 IF?A=CH"<"A=A+1;U=4
4040 IF?A=CH">"A=A+1;U=U+1
4050 IF?A=CH"="A=A+1;U=U+2
4060 IFU=0 OR U>6 P.$6"ILLEGAL TEST";G.o
4070 GOS.u;GOS.e
4080 GOS.v;M=V;GOS.v;U=V
4090 GOS.v;L=V;GOS.l
4100 IFM>0[CMP M;]
4110 IFM<=0[CMP @-M;]
4120 L=M;GOS.r;GOS.g;G.(4200+U)
4201 [BEQ P+4;BCS P+5;];G.z
4202 [BEQ P+5;];G.z
4203 [BCS P+5;];G.z
4204 [BCC P+5;];G.z
4205 [BNE P+5;];G.z
4206 [BCC P+7;BEQ P+5;];G.z
4210z[JMP LLG;]
4220 U=G;GOS.u;H=0;R.
5020uSSS=U;S=S+1;IFS<21 R.
5021 P.$6"STACK FULL";G.o
5030vS=S-1;IFS>=0V=SSS; R.
5031 P.$6"STACK ERROR";G.o
5040bIF?A=32 DO A=A+1;U.?A<>32
5041 IF?A=13A=A+3;P.$A';G.b
5042 IF?A=CH"{"DOA=A+1;U.?A=CH"}";A=A+1;G.b
5043 R.
5070gG=G+1;IF G<20 R.
5072 P.$6"TOO MANY LABELS";G.o
5100tREM TEMP. LOC.
5110 N=-1;DO N=N+1; IF N>20P.$6"NOT ENOUGH TEMP";G.o
5120 U.TTN=0
5130 T=N+RR2; TTN=T; U=T; H=T; G.u
6000xREM READ SYMBOL
6010 GOS.b;N=-1;DO N=N+1; N?X=A?N
6020 U.A?N>CH"Z"ORA?N<CH"A"ORN=7
6030 IF N=0 R.
6040 IF N<7 N?X=#D;A=A+N;R.
6050 P.$6"SYMBOL TOO LONG";G.o
7000lREM LOAD ACCUMULATOR
7010 IFL=H AND L>0 G.r
7020 GOS.m
7030 IFL<=0 [LDA @-L;];R.
7040 [LDA L;];G.r
7100mREM STORE ACCUMULATOR
7200 IFH>0[STA H;];H=0
7210 R.
7300rREM RELEASE VARIABLE
7310 IF L>=RR2 AND L<RR3;TT(L-RR2)=0
7320 R.
9000oREM ERROR
9010 N=A;X=0;DO N=N-1;X=X+1;U.?N=13;@=5
9020 P.'N?1*256+N?2,$N+3'
9030 F.N=0TOX+1;P." ";N.;P."^"';E.
