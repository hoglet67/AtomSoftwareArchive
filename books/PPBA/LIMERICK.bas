  10 REM ... LIMERICKS ...
  15 DIM S(100),G(32),H(32)
  20 P=1000;PRINT "A "
  30 GOSUB s;W=R;GOSUB s;X=R;GOSUB s;Y=R
  40 PRINT "FROM "
  50 GOSUB s;Z=R;PRINT '
  60 PRINT "ONCE ";GOSUB s;$G=$C
  70 GOSUB s;R=Z;GOSUB t;PRINT '
  80 R=Y;GOSUB t;$H=$C;PRINT $G,"SO "
  90 GOSUB s;T=R;PRINT '"THAT ",$H;GOSUB s;R=T; GOSUB t; PRINT '
 110 PRINT "THIS ";P=1000;R=W;GOSUB t;R=X;GOSUB t; R=Y;GOSUB t
 120 PRINT "OF ";R=Z;GOSUB t;PRINT "."'
 140 END
 200sR=ABSRND%6
 210tGOSUB (P);A=0;IF R=0 G.u
 220 FOR N=1 TO R;GOSUB c;NEXT;A=A+1
 240uC=A+S;GOSUB c;$S+A=" "
 250 PRINT $C;P=P+100; RETURN
 300cDO A=A+1;UNTIL S?A=CH","OR S?A=CH""; RETURN
1000 REM WORDS
1001 $S="SORDID,GRACEFUL,WILY,VICIOUS,SPARKLING, REALLY"; RETURN
1100 $S="GREEN,YOUNG,VILE,BLAND,OLD,WILD"; RETURN
1200 $S="DUCHESS,GROCER,GLUTTON,FLAUTIST,LAUNDRESS,SAILOR"; RETURN
1300 $S="WEMBLEY,SPAIN,CHAD,SPEKE,KINGS,FRANCE"; RETURN
1400 $S="WANTED,FOLLOWED,COUNTED,"
1401 $S+LENS="DEMOLISHED,COLLECTED,SWALLOWED"; RETURN
1500 $S="SOME STAMPS,A STOAT,A NUDE,"
1501 $S+LENS="SOME CAKES,A FROG,SOME MOULD"; RETURN
1600 $S="AND FELT TREMBLY,ON A TRAIN,AND WENT MAD,"
1601 $S+LENS="TWICE A WEEK,AND GREW WINGS,IN A TRANCE"; RETURN
1700 $S="SHE,HE,SHE,HE,SHE,HE"; RETURN
1800 $S="QUICK,SLOW,FEW,HARD,LATE,LONG"; RETURN
1900 $S="NOTICED,FOLLOWED,ASKED FOR,"
1901 $S+LENS="LOOKED FOR,WANTED,LONGED FOR"; RETURN
2000 $S="A BRICK,SOME DOUGH/A SCREW,"
2001 $S+LENS="SOME LARD,A PLATE,KING KONG"; RETURN