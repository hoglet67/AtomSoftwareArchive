 10 REM ...SURFACE...
 20 DIM Q255,R255,O22,P22
 30 FINPUT"VIEW FROM:"'"X="%L,"Y="%M,"Z="%N
 40 %S=%L*%L+%M*%M;%R=SQR%S
 50 %T=%S+%N*%N; %Q=SQR%T
 60 FOR I=0TO255; R?I=191; Q?I=0;NEXT
 70 E=10; CLEAR4
 80 FOR F=-E TO E+1; FOR G=-E TOE+1; U=X; V=Y
 90 %O=SQR(F*F+G*G)
100 %X=-G; %Y=-F; %Z=COS(%O/1.5)*EXP(-%O/5)*5
110 %O=(%T-%X*%L-%Y*%M-%Z*%N)*%R
120 FIF %O<0.1 GOTO m
130 X=%(400*(%Y*%L-%X*%M)*%Q/%O)+128; IF X<0 X=0
140 Y=%(500*(%Z*%S-%N*(%X*%L+%Y*%M))/%O)+96
180 IF Y<0 Y=0
190 IF Y>191 Y=191
200 IF G=-E Q?X=Y; R?X=Y
210 IF G+E A=G&1; GOSUB d
220 IF F+E U=O?(G+E); V=P?(G+E); A=F&1; GOSUB d
230mO?(G+E)=X; P?(G+E)=Y
240 NEXT; NEXT
250 PRINT $7; DO UNTIL 0
400dM=Y-V; N=ABS(X-U); IF N=0 RETURN
410 S=(X-U)/N
500 FOR I=1 TO N; J=U+I*S; K=V+I*M/N
503 IFJ<0 OR J>255 GOTO e
505 IF K>Q?J; Q?J=K
510 IF K<R?J; R?J=K
513 IF A=0 GOTO e
515 MOVE (J-S),(Q?(J-S)); DRAW J,(Q?J)
517 MOVE (J-S),(R?(J-S)); DRAW J,(R?J)
520eNEXT; RETURN
