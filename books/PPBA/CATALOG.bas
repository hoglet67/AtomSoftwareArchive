   5 REM ... CATALOGUE ...
  10 DIM M(64),T(3),F(-1)
  20 !T=0;Z=#3BFF
  30 DO PRINT $12"command"
  40 INPUT $M
  50 IF?M=CH"*"X=!T;GOSUB p;GOTO n 
  60 IF?M=CH">"GOTO a
  70 N=M;X=T;S=1;E=0;GOSUB s 
  80 IF E GOSUB q;GOTO n
  90 PRINT "NOT FOUND"';GOTO n
 100aN=M+1;X=T;E=0;S=0;GOSUB s 
 110 IF E:1 UNTIL 0
 120 PRINT $N" ALREADY EXISTS" 
 130nLINK#FFE3;UNTIL 0
1000sREM SEARCH TREE
1010 IF !X=0 GOTO b
1020 X=!X;J=X+8;GOSUB c
1030 IF E RETURN
1040 IF C X=X+4
1050 GOTO s
1100bIF S RETURN
1110 Y=X;!X=F;X=!X;!X=0;X!4=0
1120 X=X+8;$X=$N;X=X+LENX+1
1130 INPUT$X;X=X+LENX+1
1140 IF X&#FFFF>Z PRINT "NO ROOM";!Y=0;LINK #FFE3;RETURN
1150 F=X;RETURN
2000cREM COMPARE $J AND $N
2010 I=-1;DO I=I+1
2020 UNTIL J?I<>N?I OR N?I=13
2030 C=(J?I<N?I)
2040 IF S E=(N?I=13);RETURN
2050 E=($J=$N);RETURN
4000pREM PRINT TREE
4010 IF X=0 RETURN
4020 !F=X;F=F+2;X=!X;GOSUB p
4030 F=F-2;X=!F
4040 J=X+8;GOSUB q;X=X+4
4050 X=!X;GOTO p
5000qREM PRINT RECORD
5010 PRINT $J;DOPRINT " ";UNTIL COUNT=20 
5020 J=J+LENJ+1;PRINT $J';RETURN
