10 REM ... ASSIGNMENT ...
20 DIM SS(20),LL(20),II(30),JJ(30)
30 DIM X(7),TT(20),RR(2)
35 RR0=#3A00; RR1=#50; RR2=#80
40 F.N=0TO30; DIMI(6); IIN=I; JJN=RR0; N.
115 F.N=0TO20; TTN=0; LLN=RR0; N.
140 Z=#100; G=0; I=0; P=RR0; S=0; R=0; H=0; T=0
200 DO INPUT$Z;A=Z
210 GOS.s;GOS.m
220 UNTIL 0
1000sREM STATEMENT
1010 GOS.b;GOS.x
1110 GOS.b
1135 GOS.j
1160 GOS.d;H=V;GOS.m;R.
1180dIF?A<>CH"=" P.$6 "NO =";G.o
1190 A=A+1;GOS.e;GOS.v;L=V;GOS.l;GOS.v;R.
2000iREM IDENTIFIER
2010 GOS.x
2030jIF N=0 R.
2040 GOS.y
2050 IFN=I;I=I+1;R=R+1;JJN=R+RR1
2070 IFI>30P.$6"TOO MANY VARIABLES";G.o
2080 U=JJN;GOS.u;R.
2100cREM CONSTANT
2105 GOS.b
2110 N=-1;C=0;DO D=C;N=N+1;C=C*10
2120 C=C+A?N-CH"0"
2130 U.A?N<CH"0"ORA?N>CH"9"
2140 IFN=0 R.
2150 A=A+N
2160 U=-D;GOS.u;N=1;R.
2400yREM LOOKUP
2410 $III=$X;N=-1
2420 DO N=N+1;U.$IIN=$III;R.
3000eREM EXPRESSION
3010 GOS.b;GOS.f
3015 GOS.b
3020 IF?A=CH"+"OR?A=CH"-"OR?A=CH"&"OR ?A=CH"|"O=?A;A=A+1;G.3035
3025 IF((?A=CH">"A.A?1=CH">")OR(?A=CH "< "A.A? 1=CH "< " )):1 R.
3030 O=?A;A=A+2
3035 U=O;GOS.u
3040 GOS.f;GOS.v;U=V;GOS.v;O=V;GOS.v;L=V;GOS.l
3045 IF U<=0 G.3070
3050 IFO=CH"+"[CLC;ADC U;]
3055 IFO=CH"-"[SEC;SBC U;]
3060 IFO=CH"|"[ORA U;]
3065 IFO=CH"&"[AND U;]
3068 G.3190
3070 IFO=CH"+" [CLC;ADC @-U;]
3075 IFO=CH"-"[SEC;SBC @-U;]
3080 IFO=CH"|"[ORA @-U;)
3085 IFO=CH"&"[AND @-U;]
3160 IFO=CH"<"F.N=1TO-U;[ASL A;];N.
3180 IFO=CH">"F.N=1TO-U;[LSR A;];N.
3190 L=U;GOS.r;GOS.t
3195 G.3015
3600fREM FACTOR
3610 GOS.x;IFN=0G.3630
3620 GOS.j
3625 R.
3630 GOS.c;IF N R.
3635 IF?A<>CH"(" P.$6"BRACKET MISSING";G.o
3640 A=A+1;GOS.e;GOS.b
3650 IF?A<>CH")" P.$6"BRACKET MISSING";G.o
3660 A=A+1;R.
5020uSSS=U;S=S+1;IFS<21 R.
5021 P.$6"STACK FULL";G.o
5030vS=S-1;IFS>=0V=SSS; R.
5031 P.$6"STACK ERROR";G.o
5040bIF?A=32 DO A=A+1;U.?A<>32
5043 R.
5100tREM TEMP. LOC.
5110 N=-1;DO N=N+1; IF N>20P.$6"NOT ENOUGH TEMP";G.o
5120 U.TTN=0
5130 T=N+RR2; TTN=T; U=T; H=T; G.u
6000xREM READ SYMBOL
6010 GOS.b;N=-1;DO N=N+1; N?X=A?N
6020 U.A? N>CH "Z "ORA?N<CH "A" ORN=7
6030 IF N=0 R.
6040 IF N<7 N?X=#D;A=A+N;R.
6050 P.$6"SYMBOL TOO LONG";G.o
7000lREM LOAD ACCUMULATOR
7010 IFL=H AND L>0 G.r
7020 GOS.m
7030 IFL<=0 [LDA @-L;];R.
7040 [LDA L;];G.r
7100mREM STORE ACCUMULATOR
7200 IFH>0[STA H;];H=0
7210 R.
7300rREM RELEASE VARIABLE
7310 IF L>=RR2 AND L<RR3;TT(L-RR2)=0
7320 R.
9000oREM ERROR
9020 P.'$Z'
9030 F.N=Z TOA-2;P." ";N.;P."^"';E.
