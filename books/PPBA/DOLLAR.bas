5 REM ... SILVER DOLLAR GAME ...
10 DIM PP5,TT2,Q0,R-1
20 PP0=-1; FOR N=1 TO 5; PPN=PP(N-1)+ABSRND%8+1; NEXT
25 P=R;PRINT$21;[JSR#FFE3;STAQ;RTS;] 
28 PRINT$6$12;GOSUB p
30aGOSUB w
35 IFW PRINT'"I WIN!"';END
40 GOSUB m;GOSUB w
50 IFW PRINT'"YOU WIN!"';END
60 GOSUB i;GOTO a
1000iS=0;FOR N=0 TO 2
1010 TTN=PP(2*N+1)-PP(2*N)-1;S=S:TTN;NEXT 
1020 IF S=0 GOTO r
1030 N=-1;DO N=N+1; T=TTN-(S:TTN) 
1040 UNTIL T>0;N=2*N+1
1050 GOTO j
1100rN=0;DO N=N+1;UNTIL PPN-PP(N-1)>=2
1120 T=(PPN-PP(N-1))/2
1200jGOSUB c
1250 PPN=PPN-T
1260 FOR J=1 TO T
1270 FOR K=1TO999;NEXT;GOSUB b; NEXT; PRINT$30;RETURN
2000pN=0;PRINT$30" ";FOR J=1 TO 5
2010 IF N<PPJ DOPRINT".";N=N+1;UNTILN=PPJ
2020 PRINT$(J+CH"@");N=N+1;NEXT;DOPRINT".";N=N+1; UNTILN=30
2030 PRINT$30;RETURN
3000mLINKR;IF?Q<CH"A"OR?Q>CH"E" PRINT$7;GOTO m
3050 N=?Q-CH"@"
3060 IF PPN-PP(N-1)<2 PRINT$7;GOTO m
3070 GOSUB c
3075 GOTO v
3080qLINKR;IF?Q=13 RETURN
3090 IF PPN-PP(N-1)<2 PRINT$7;GOTO q
3100vPPN=PPN-1
3110 GOSUB b;GOTO q
4000wW=1;FOR N=1TO5;IFPPN<>N-1 W=0
4100NEXT;RETURN
5000cPRINT$30;FORJ=0 TO PPN;PRINT$9;NEXT;RETURN
6000bPRINT"."$8$8$(N+CH"@")$8;RETURN
