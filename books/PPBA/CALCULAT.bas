   5 REM ... CALCULATOR ...
  10 DIM M(64),SS(10),F(-1)
  20 S=0;SSS=F; Z=CH"0"
  30 DO INPUT$M
  40 IF?M>=CH"0"AND?M<=CH"9" $F=$M; GOSUB u;GOSUB f;GOTO x
  45 IFS>0AND?M=CH""""A=SSS; D=SS(S-1);GOSUB u;GOTO w
  46 IFS<2 PRINT"STACK EMPTY"';GOTO x
  48 D=SSS;B=SS(S-1);A=SS(S-2)
  50 IF?M=CH"*" GOSUB m;GOTO z
  55 IF?M=CH"+" GOSUB a;GOTO z
  58 IF?M=CH"/" GOSUB d;GOTO z
  60 PRINT"ERROR"';GOTO x
  90zS=S-1
  92wD=D-1;DO D=D+1; UNTIL?D<>Z OR D?1=13
  95 $A=$D; F=A;GOSUB f
 100 PRINT " = "$SS(S-1)'
 110xUNTIL 0
 500fF=F+LENF+1; SSS=F; RETURN
1000mL=LENA+LENB;FOR N=0 TO L-1; D?N=Z;NEXT;D?L=13
1010 FOR J=LENB-1 TO 0 STEP -1;C=O;G=B? J-Z
1020 V=D+J+1
1030 FOR L=LENA-1 TO 0 STEP -1;H=A?L-Z
1040 Q=G*H+C+(V?L-Z)
1050 V?L=Q%10+Z;C=Q/10;NEXT
1060 V?L=C+Z
1070 NEXT; RETURN
2000aW=A;V=B; J=LENA-LENB;IFJ<0 W=B;V=A; J=-J
2010 $(D+1)=$W;?D=Z;C=0;W=D+J+1
2020 FOR L=LEN V-1 TO 0 STEP -1
2030 Q=W?L+V?L-2*Z
2040 W?L=Q%10+Z;C=Q/10
2050 NEXT; W?L=W?L+C; RETURN
4000dFOR J=0 TO LEN A-LEN B; W=A+J; V=-1
4010 DO V=V+1;C=1
4020 FOR L=LEN B-1 TO -J STEP -1
4025 Q=Z; IF L>=0 Q=B?L
4030 Q=W?L-Q+C+9
4032 W?L=Q%10+Z; C=Q/10
4035 NEXT
4040 UNTIL C=0
4050 FOR L=LEN B-1 TO -J STEP -1
4055 Q=Z; IF L>=0 Q=B?L
4060 Q=W?L+Q-2*Z+C
4065 W?L=Q%10+Z;C=Q/10;NEXT
4070 D?J=V+Z;NEXT;D?J=13 
4080 RETURN
6000uIF S>10 PRINT"STACK FULL"';RETURN 
6010 S=S+1;RETURN
