
 10 REM OTHELLO
100 M=1;H=8;C=3;DIM A(99),NN(7),B(9)
110 NN(0)=11;NN(1)=10;NN(2)=9;NN(3)=1
120 NN(4)=-1;NN(5)=-9;NN(6)=-10;NN(7)=-11
130 P.$12;?#E1=0;P."        A B C D E F G H"'
140 @=7;FOR I=1 TO 8;P.I" * * * * * * * *"';NEXT
150 FOR I=0 TO 99;A?I=0;NEXT
160 T=C;P=44;GOSUB a;P=55;GOSUB a
170 T=H;P=45;GOSUB a;P=54;GOSUB a
200 REM YOUR MOVE
210 GOSUB b;IF ($B="" AND M=0) OR Y+Z=64 GOTO 900
230 INPUT"YOUR TURN (E.G. 3D) "$B;IF $B="" GOTO 300
240 IF ?B<CH"1" OR ?B>CH"8" OR B?1<CH"A" OR B?1>CH"H" GOTO 260
250 P=(?B-CH"0")*10+B?1-CH"A"+1;T=H;F=0;GOSUB c;IF S>0 GOTO 270
260 P.$11;GOSUB g;GOTO 230
270 F=1;GOSUB c
300 REM COMPUTER'S MOVE
310 GOSUB b;IF ($B="" AND M=0) OR Y+Z=64 GOTO 900
320 M=0;D=0;F=0;T=C;P."LET ME THINK ...."
330 FOR U=1 TO 8;FOR V=1 TO 8;P=U+10*V;GOSUB c;IF S=0 GOTO 365
340 IF U=1 OR V=8 S=S*2
345 IF V=1 OR V=8 S=S*2
350 IF U=2 OR U=7 OR V=2 OR V=7 S=S/2
355 IF S=D D=D-ABSRND%2
360 IF S>D D=S;M=P
365 NEXT V;NEXT U
370 GOSUB g;IF M>0 F=1;P=M;GOSUB c
380 IF M=0 P."I CNA'T MOVE";FOR I=1 TO 25;WAIT;NEXT
390 GOTO 200
900 REM WRAP IT UP
910 IF Y>Z P."YOU WON - CONGRATULATIONS"'
920 IF Y=Z P."** A DRAW **"'
930 IF Y<Z P."I WON - OF COURSE"'
940 ?#E1=#80;END
1000a REM PLACE PIECE
1010 X=2*(P%10)+32*(P/10);X?#8006=128+X?#8006
1020 FOR I=1 TO 30;WAIT;NEXT
1030 A?P=T;X?#8006=T;RETURN
2000b REM ADD SCORES
2010 Y=0;Z=0
2020 FOR I=11 TO 88
2030 IF A?I=H Y=Y+1
2040 IF A?I=C Z=Z+1
2050 NEXT
2060 P.$30;FOR I=1 TO 10;P.$10;NEXT
2070 FOR I=1 TO 3;GOSUB g;P.$10;NEXT
2080 FOR I=1 TO 3;P.$11;NEXT
2090 @=0;P."YOU HAVE "Y", I HAVE "Z'';RETURN
3000c REM EXAMINE POSITION & MOVE IF F=1
3010 R=P;S=0;IF A?P<>0 RETURN
3015 IF F=1 GOSUB a
3020 FOR J=0 TO 7;Q=R;P=R;K=0
3030d Q=Q+NN(J);IF A?Q=0 GOTO f
3040 IF A?Q=T GOTO e
3050 K=K+1;GOTO d
3060e S=S+K;IF F=0 OR K<1 GOTO f
3070 FOR L=1 TO K;P=P+NN(J);GOSUB a;NEXT
3080f NEXT J;RETURN
4000g REM CLEAR LINE
4010 P.$13"                                "$11;RETURN
