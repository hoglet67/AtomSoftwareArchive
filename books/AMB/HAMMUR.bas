
10 REM HAMMURABI
15 GOSUB 1010
20 H=100;S=3000;A=1000;Y=0;@=0;P.$12
100 P.'"O HAMMURABI, KING OF SUMARIA,"'
120 P."YOUR SUBJECTS NOW NUMBER "H","'
130 P."YOUR KINGDOM COVERS "A" ACRES,"'
140 P."YOU HAVE "S" BUSHELS OF GRAIN."''
200 L=20+RND%5;P."LAND IS "L" BUSHELS PER ACRE,"'
210 INPUT"HOW MANY ACRES DO YOU BUY "E;IF E*L>S GOSUB a;G.210
220 IF E>0 GOTO 260
230 GOSUB a
240 INPUT"HOW MANY ACRES DO YOU SELL"E;E=-E
250 IF (-E)>A GOSUB a;GOTO 240
260 A=A+E;S=S-L*E
270 INPUT"HOW MANY BUSHELS FOR FOOD "F;IF F>S GOSUB a;GOTO 270
280 S=S-F
290 INPUT"HOW MANY BUSHELS FOR SEED "P;IF P>S GOSUBa;GOTO290
300 S=S-P;IF P>2*A P=2*A
310 IF P>25*H P=25*H
320 R=ABSRND%(1+S/2);C=P/4+ABSRND%(1+P*4);S=S+C-R;D=H-F/15
330 D=H-F/15;IF D<1 D=0
340 H=H-D;N=H/20+ABSRND%(1+H/20);H=H+N
350 Z=0;IF RND%4=3 Z=H/3+RND%(1+H/4)
360 H=H-Z;Y=Y+1
400 P.$12;FOR I=1 TO 50;WAIT;NEXT I
410 P."O KING,I BEG TO REPORT THAT"'
420 P."IN YEAR "Y" OF YOUR REIGN"'
430 P."YOU HARVESTED "C" BUSHELS"'
440 IF R>0 P."BUT RATS ATE "R" BUSHELS"'
450 P.D" PEOPLE STARVED, "N" WERE BORN"'
480 IF Z>0 P.Z" DIED OF PLAGUE"'
490 IF H>0 GOTO 100
500 P.''"AS YOU HAVE NO SUBJECTS LEFT,"'
510 P."THIS IS THE END OF YOUR REIGN"'';END
1000aP.$11$13"                                "$13$11;R.
1010 P.$12"HAMMURABI"'"========="''"YOU PLAY THE PART OF "
1015 P."HAMMURABI; KING OF ANCIENT SUMARIA. EACH YEAR YOUR FAI"
1020 P."THFUL SERVANT - ATOM - TELLS YOU HOW BIG YOUR KINGDOM "
1025 P."IS, HOW MANY SUBJECTS YOU HAVE,AND HOW MUCH GRAIN IS "
1030 P."LEFT FROM THE HARVEST AND FROM PREVIOUS YEARS."''
1035 P."PRESS KEY TO CONTINUE"';LINK #FFE3;P.$12
1040 P."YOU THEN HAVE TO DECIDE WHETHER TO TRADE LAND FOR GRAI"
1045 P."N WITH NEIGHBOURING KINGDOMS, HOW MUCH TO ALLOCATE AS "
1050 P."FOOD FOR THE COMING YEAR, AND HOW MUCH GRAIN IS TO BE "
1055 P."USED AS SEED."''"PRESS KEY TO CONTINUE"'
1056 LINK #FFE3;P.$12
1057 P."YOUR SUBJECTS WILL DIE OF STARVATION "
1060 P."IF YOU DON'T ALLOCATE ENOUGH FOOD FOR THE COMING YEAR "
1065 P."(ABOUT 15 BUSHELS EACH), BUT IF YOU GIVE THEM TOO "
1070 P."MUCH THE SURPLUS IS WASTED. EACH YEAR'S HARVEST DEPEND"
1075 P."S ON THE AMOUNT OF SEED USED, BUT PLANTING MORE THAN 2"
1080 P." BUSHELS PER ACRE IS WASTEFUL, AS IS ALLOCATING MORE "
1085 P."THAN 25 BUSHELS OF SEED TO EACH OF YOUR SUBJECTS."''
1090 P."PRESS KEY TO CONTINUE"';LINK #FFE3;P.$12
1095 P."THE HARVEST IS ALSO SUBJECT TO A LARGE RANDOM FACTOR "
1100 P."(THE WEATHER?). RATS CAN DEVASTATE YOUR GRAIN STORES, "
1105 P."AND YOUR KINGDOM IS SUBJECT TO PLAGUES ABOUT EVERY "
1110 P."SEVENTH YEAR."''"PRESS KEY TO START"'
1115 LINK #FFE3;RETURN
