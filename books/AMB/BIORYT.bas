100 P.$12"* ATOMIC BIORYTHM CALCULATER *"
110 P.''"ENTER YOUR DATE OF BIRTH"''
120 E=0;GOSUB 500;E=-E
130 P.'"NOW ENTER TODAY'S DATE"''
150 GOSUB 500;E=E-14
200 P.$12;FOR I=1 TO 6;P.';NEXT;P." "
210 FOR I=1 TO 4;P."!......";NEXT;P."!"'
220 FOR I=1 TO 7;P.';NEXT
230 P." PREV 2 WKS  TODAY  NEXT 2 WKS"'
250 FOR I=0 TO 12;X=32*I+#800F;?X=#21;NEXT
300 P=#10;C=23;GOSUB 600
310 P=#5;C=28;GOSUB 600
320 P=#9;C=33;GOSUB 600
400 @=0;P."     YOU ARE "E+14" DAYS OLD"
410 END
500 REM GET # OF DAYS
510 INPUT"YEAR "Y,"MONTH (1-12) "M,"DAY (1-31) "D
515 E=E+D
520 IF M>1 E=E+31;IF M>2 E=E+28
525 IF M>3 E=E+31;IF M>4 E=E+30
530 IF M>5 E=E+31;IF M>6 E=E+30
535 IF M>7 E=E+31;IF M>8 E=E+31
540 IF M>9 E=E+30;IF M>10 E=E+31
545 IF M>11 E=E+30
550 IF M>2 IF Y%4=0 IF Y%100<>0 E=E+1
560 Y=Y-1800
570 FOR I=0 TO Y-1;E=E+365
580 IF I%4=0 IF I%100<>0 E=E+1
590 NEXT;RETURN
600 REM PLOT A CURVE
610 FOR I=0 TO 28
620 X=%(6.5*SIN(2*PI*(E+I)/C))
630 X=#80C1-32*X+I;?X=P
640 NEXT;RETURN

