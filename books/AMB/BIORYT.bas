
10 P.$12"* ATOMIC BIORYTHM CALCULATER *"''
20 P."THIS PROGRAM PLOTS GRAPHS OF YOUR PHYSICAL (P), "
30 P."EMOTIONAL (E) AND INTELLECTUAL (I) CYCLES FOR A FOUR "
40 P."WEEK PERIOD."'"THE CRITICAL DAYS ARE THOSE WHEN THE "
50 P."CYCLE CROSSES THE CENTER LINE."'"AS FINALE, THE PROGRAM "
60 P."CALCULATES HOW MANY DAYS YOU HAVE LIVED."'
70 P.'"ENTER YOUR DATE OF BIRTH"''
80 E=0;GOSUB 210;E=-E
90 P.'"NOW ENTER TODAY'S DATE"''
100 GOSUB 210;E=E-14
110 P.$12;FOR I=1 TO 6;P.';NEXT;P." "
120 FOR I=1 TO 4;P."!......";NEXT;P."!"'
130 FOR I=1 TO 7;P.';NEXT
140 P." PREV 2 WKS  TODAY  NEXT 2 WKS"'
150 FOR I=0 TO 12;X=32*I+#800F;?X=#21;NEXT
160 P=#10;C=23;GOSUB 350
170 P=#5;C=28;GOSUB 350
180 P=#9;C=33;GOSUB 350
190 @=0;P."     YOU ARE "E+14" DAYS OLD"
200 END
210 REM GET # OF DAYS
220 INPUT"YEAR "Y,"MONTH (1-12) "M,"DAY (1-31) "D
230 E=E+D
240 IF M>1 E=E+31;IF M>2 E=E+28
250 IF M>3 E=E+31;IF M>4 E=E+30
260 IF M>5 E=E+31;IF M>6 E=E+30
270 IF M>7 E=E+31;IF M>8 E=E+31
280 IF M>9 E=E+30;IF M>10 E=E+31
290 IF M>11 E=E+30
300 IF M>2 IF Y%4=0 IF Y%100<>0 E=E+1
310 Y=Y-1800
320 FOR I=0 TO Y-1;E=E+365
330 IF I%4=0 IF I%100<>0 E=E+1
340 NEXT;RETURN
350 REM PLOT A CURVE
360 FOR I=0 TO 28
370 X=%(6.5*SIN(2*PI*(E+I)/C))
380 X=#80C1-32*X+I;?X=P
390 NEXT;RETURN
