
10 REM MOONLANDER
20 DIM P(-1);GOSUB 500;P.$21
30[;JSR #FE71;STY #80;RTS;];P.$6
100 J=#1B8020;K=#1B8080;L=#1B80E0
110 M=#1B8140;N=#B002;O=#DE
120 D=0;A=45000;V=0;H=300;F=1250
130 T=1000;Y=A/T+2;X=D/T
140 CLEAR 0;@=5;MOVE 0,0;DRAW 63,0;MOVE 0,1
150 DRAW 63,1;MOVE 31,2;DRAW 33,2
160 !O=#1D8000;P."ALT"
170 !O=#1D8060;P."H-V"
180 !O=#1D80C0;P."V-V"
190 !O=#1C8120;P."FUEL"
200a FOR U=1 TO 5;IF A<1 A=0;G.c
210 WAIT;PLOT15,X,Y;X=D/T;Y=A/T+2
220 WAIT;PLOT13,X,Y;?N=#D3
230 LINK TOP;R=?#80;IF F=1 F=0;G.b
240 IF R=53 V=V-5;F=F-5;?N=#D7
250 IF R=50 H=H-3;F=F-3;?N=#D7
260 IF R=38 H=H+3;F=F-3;?N=#D7
270b A=A-V;D=D+H;V=V+2;N.
280c !O=J;P.A/100;!O=K;P.H;!O=L;P.V;!O=M;P.F
290 IF A>0 G.a
300 IF ABSV<50 IF ABSH<20 G.d
310 FOR I=1 TO 4;MOVE(X-6),4
320 PLOT5,X,0;?N=?N:4;PLOT1,6,4;?N=?N:4;WAIT
330 PLOT7,X,0;?N=?N:4;PLOT3,(-6),4;?N=?N:4
340 NEXT;MOVE X,0;PLOT 3,0,3
350 !O=#8080;P."ANOTHER CRATER "
360 IF ABSV<100 G.e
370 !O=#80C0;P."WHERE WAS THE PILOT ?";G.e
400d !O=#8080;S=ABS(H*V)
410 IF S<300 P."SMOOTH - -"
420 IF S>299 P."BUMPY ! ! "
430 !O=#80C0;@=0;X=ABS(X-32)
440 IF X>1 P."BUT "X" KM FROM BASE"
450 IF X>5 P."   walk"
460 IF X>1 OR S>200 G.e
470 P."HELLO BUCK ROGERS "
499e ?#E1=#80;GOTO 514
500 P.$12"       *** MOONLANDER ***"'"U BESTUURT EEN RAKET, "
501 P."WELKE OP DE MAAN MOET LANDEN. DE **!!**!!** COMPUTER "
502 P."HEEFT WEER EENS GEFAALD (EN DE MOTOR LOOPT SLECHT)."'
503 P."BIJ DE START BENT U OP 450 METER HOOGTE EN U BEWEEGT "
504 P."ZICH HORIZONTAAL VOORT MET EEN SNELHEID VAN 300 M/S EN "
505 P."BEGINT UIT UW OMLOOPBAAN TE VALLEN. U MOET PROBEREN OP "
506 P."DE BASIS TE LANDEN MET EEN HORIZONTALE SNELHEID KLEINER"
507 P." DAN 20 M/S EN EEN VERTIKALE SNELHEID KLEINER DAN"
508 P." 50 M/S."''"DRUK TOETS";LINK#FFE3;P.$12
509 P."CONTROLS"'"--------"''"U HEEFT DE VOLGENDE TOETSEN:"'
510 P."F - VOORUIT"'"R - ACHTERUIT"'"U - REMMEN"''
511 P."DE INSTRUMENTEN WORDEN RECHTS OP HET SCHERM GEGEVEN:"'
512 P."HOOGTE, SNELHEDEN EN BRANDSTOF"''
513 P."DRUK TOETS OM TE BEGINNEN";LINK#FFE3;RETURN
514 FOR N=1 TO 300;WAIT;NEXT N;PRINT $12
515 INPUT"NOG EENS (JA=1;NEE=0)"Q
516 IF Q=0 THEN PRINT'''"           TOT ZIENS "''';END
517 GOTO 100
