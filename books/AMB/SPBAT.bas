
1 REM SPACE BATTLE - BY T. MABBS
10 P.$12"*** SPACE BATTLE ***"''
15 P."KEY 'B' TO GO RIGHT"'
20 P."    'C'       LEFT"'
25 P."    'D'       DOWN"'
30 P."    'E'       UP"'
35 P."    'F' TO FIRE"''
50 INPUT"NUMBER OF ALIEN SHIPS (1-20)"A;IF A<1 OR A>20 GOTO 50
60 INPUT"LEVEL OF SKILL (1-6)"L;IF L<1 OR L>6 GOTO 60
70 O=56+L;P=72-L;Q=24+L;R=40-L;U=1;V=1;S=0
80 P."PRESS ANY KEY TO START";LINK #FFE3
100 GOSUB d
200e PLOT 14,X,Y;F=X;G=Y;GOSUB b
210 IF ABSRND%100<L GOSUB 500
220 IF ?#80=0 GOSUB 300
230 IF ?#81=0 Y=Y-3
250 IF ?#82=0 Y=Y+3
260 IF ?#83=0 X=X+3
270 IF ?#84=0 X=X-3
280 X=(X+U)&127;Y=(Y+V)&63
290 PLOT 14,F,G;GOTO e
300 S=S+1;FOR C=1 TO 6;?#B002=?#B002:4;NEXT
320 MOVE 32,0;PLOT 6,64,32;PLOT 6,96,0
330 MOVE 32,0;PLOT 6,64,32;PLOT 6,96,0
340 IF X>O IF X<P IF Y>Q IF Y<R GOTO 400
350 RETURN
400 MOVE (X-3),(Y-3);PLOT 5,(X+3),(Y+3)
410 MOVE (X+3),(Y-3);PLOT 5,(X-3),(Y+3)
420 FOR C=1 TO 90;?#B002=?#B002&RND:4;NEXT
430 A=A-1;IF A=0 GOTO 1000
440 GOSUB d;PLOT 14,X,Y;RETURN
500 MOVE X,Y;C=64+RND%8;D=32+RND%5;PLOT 6,C,D
510 Z=#B002;FOR K=1 TO 8;?Z=?Z:4;N.
520 MOVE X,Y;PLOT 6,C,D
530 U=-U;IF RND%2=0 V=-V
540 IF C<>64 OR D<>32 RETURN
560 @=0;P.$12"BAD LUCK!"'"YOU WERE HIT BY ENEMY FIRE"'
570 P."AND YOUR SHIELDS FAILED."'"YOU USED "S" SHOTS"'
580 P."BUT "A" ALIEN REMAINED"';GOTO 1100
800dX=ABSRND%128;Y=ABSRND%64;F=X;G=Y
810aCLEAR 1;MOVE 64,0;DRAW 64,Q;MOVE 64,R;DRAW 64,64
820 MOVE 0,32;DRAW O,32;MOVE P,32;DRAW 128,32
830 MOVE O,Q;DRAW O,R;DRAW P,R;DRAW P,Q;DRAW O,Q
840 MOVE 32,16;DRAW 32,48;DRAW96,48;DRAW 96,16;DRAW 32,16;R.
900bFOR B=1 TO 5;?#B000=?#B000&#F0+B
910 B?#7F=?#B001&8;NEXT;RETURN
1000 P.$12"WELL DONE!!"'"YOU DESTOYED THE ENEMY"'
1010 @=0;P."WITH "S" SHOT";IF S>1 P."S"
1100 P.'';INPUT"AGAIN "$TOP;IF ?TOP=CH"Y" RUN
1110 END
