
10 REM MATCHES
20 @=0
100 M=ABSRND%12+21;GOSUB 900
110 PRINT"WE TAKE TURNS TO REMOVE"'
120 PRINT"1,2 OR 3 MATCHES."''
130 PRINT"THE ONE LEFT WITH THE LAST"'
140 PRINT"MATCH LOSES."''
300 PRINT M" MATCHES, YOUR TURN"'
310 IF M=1 GOTO 510
320 INPUT"HOW MANY WILL YOU TAKE "T
330 IF T=M GOTO 500
340 IF T>3 OR T<1 OR T>M GOTO 320
400 M=M-T;IF M=1 GOTO 600
410 R=M%4;C=(R+3)%4
420 IF R=1 C=1+ABSRND%3
430 M=M-C;IF M=0 GOTO 600
440 GOSUB 900
450 PRINT"YOU TOOK  ",T'
460 PRINT"SO I TOOK ",C''
470 GOTO 300
500 PRINT $12
510 PRINT"I WON !!!";END
600 PRINT $12,"*** YOU WON ***"
610 FOR I=1 TO 10;PRINT $7;NEXT;END
900 PRINT $12;CLEAR 0;FOR K=1 TO M
910 MOVE (2*K),0;DRAW (2*K),10
920 NEXT;RETURN
