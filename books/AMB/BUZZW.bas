
10 PRINT$12"*** BUZZ-WORDS ***"''
20 DIM A(10);N=5;D=#D
21 P."DIT PROGRAMMA GENEREERT INDRUKWEKKEND KLINKENDE ZINNEN."'
22 P."DEZE ZIJN BEDOELD VOOR EEN DRUKBEZETTE SECRETARIS, OM TE"
23 P." GEBRUIKEN IN ZIJN RAPPORTEN AAN DE HOGERE LEIDING."''
100 X=?18*256
110 PRINT'"YOUR PHRASE IS;"''
120 Y=CH"a";GOSUB 500
130 Y=CH"b";GOSUB 500
140 Y=CH"c";GOSUB 500
150 PRINT''"ANOTHER "
160 INPUT $A
170 IF ?A=CH"Y" GOTO 100
180 END
500 DO DO X=X+1;U.?X=D;U.X?3=Y
510 X=X+3;Y=CH"*"
520 FOR P=1 TO 1+ABSRND%N
530 DO X=X+1;IF ?X=D X=X+3
540 UNTIL ?X=Y
550 NEXT P
600 DO X=X+1;PRINT $?X
610 UNTIL X?1=Y OR X?1=D
620 PRINT" ";RETURN
1000a*GLOBAL*PARALLEL*FEASIBLE
1010 *LATERAL*STRUCTURED
1100b*TOP-DOWN*INTEGRATED
1110 *SILICON*PRAGMATIC*MODULAR
1200c*CONCEPT*SCENARIO
1210 *PROJECTION*EVENT*PHASE
