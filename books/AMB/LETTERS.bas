
1 REM-LETTERS BY D.DINI WRITTEN 1/8/80
9 P.$21;@=0
10 DIM VV(2);P=#2802
15 FOR Q=1 TO 2
19 REM-ASSEMBLE ROUTINE TO READ KEYS 4-7
20[
30:VV0 LDX@6
40:VV1 STX#B000;LDA#B001;AND@4
50 BEQ VV2
60 INX;CPX@10;BNE VV1
70:VV2 STX#80;RTS
75]
80 NEXT Q
90 P.$6,$12;P.'''''''"        ***************"'
91 P."        *** LETTERS ***"',"        ***************"''''
92 P."          DRUK TOETS S.V.P";LINK #FFE3;GOSUB 520
100 INPUT"NO OF LETTERS"L;IF L>26 THEN GOTO 100
110 INPUT"SPEED"S
114 REM-SEED RANDOM NUMBER GENERATOR
115 ?12=0;INPUT"COURSE"C;!8=C
120 CLEAR 0;MOVE 0,0;U=1
130 DRAW 63,0;DRAW 63,47;DRAW 0,47;DRAW 0,0
134 REM-PLOT LETTERS ON SCREEN
135 FOR A=1 TO L
140 X=ABSRND%30+1;Y=ABSRND%14+1;P=#8000+32*Y+X;IF?P<>64;GOTO140
150 ?P=A;NEXT A
160 X=2;Y=45;R=1
165 REM-WAIT TILL KEY PRESSED
170 GOSUB k;IF O=10 THEN GOTO 170
180mPLOT 13,X,Y;GOSUB k;U=U+1;GOTO (K-6+181)
181 X=X+1;GOTO z
182 X=X-1;GOTO z
183 Y=Y+1;GOTO z
184 Y=Y-1
185z ?#B000=0
186 IF R>L THEN GOTO s
189 REM-CHECK IF BORDER HIT
190 IF X<1 OR X>62 OR Y<1 OR Y>46 THEN GOTO e
195 REM-CHECK IF LETTER HIT
200 V=X/2;W=15-Y/3;P=#8000+32*W+V
205 REM-CHECK IF RIGHT LETTER
210 IF ?P>26 THEN GOTO m
220 IF ?P=R R=R+1;?P=64;GOTO m
230 GOTO e
240sP.$30"WELL DONE,TOOK "U" MOVES"'',"DRUK TOETS"';LINK #FFE3
241 P.$12;GOTO 100
250e?P=42;P.$30$7"SHAME!"'',"DRUK TOETS"';LINK #FFE3
251 P.$12;GOTO 100
489 REM-GET KEY & WAIT
490kFOR T=10 TO S STEP -1
500 LINK VV0;O=?#80;IF O<10 K=O
510 WAIT;NEXT;RETURN
520 P.$12,"DIT PROGRAMMA TEKENT EEN AANTAL LETTERS OP HET "
521 P."SCHERM. U MOET DEZE UITVEGEN DOOR ER MET DE CURSOR "
522 P."OVERHEEN TE GAAN. INDIEN U DE RAND VAN HET SCHERM RAAKT"
523 P." OF DE LETTERS NIET IN ALFABETISCHE VOLGORDE UITVEEGT, "
524 P."DAN EINDIGT HET SPEL EN U VERLIEST."''
525 P."DRUK TOETS"';LINK #FFE3;P.$12
526 P."U HEEFT DE VOLGENDE CONTROLE-TOETSEN:"'
527 P."4 - OMLAAG"',"5 - OMHOOG"',"6 - LINKS"',"7 - RECHTS"''
528 P."DE CURSOR LAAT EEN SPOOR NA OM HET TE BEMOEILIJKEN."''
529 P."DRUK TOETS"';LINK #FFE3;P.$12
530 P."DE MOEILIJKHEIDSGRAAD IS NAAR KEUZE:"''
531 P."HET AANTAL LETTERS (1-26)"',"DE SNELHEID (1-10)"''
532 P."GEEFT U BIJ DE VRAAG 'COURSE?' STEEDS HETZELFDE GETAL "
533 P."IN, DAN KRIJGT U STEEDS HETZELFDE PARCOURS."''
534 P."DRUK TOETS"';LINK #FFE3;P.$12;RETURN
