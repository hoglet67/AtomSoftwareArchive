
1 P.$12'''"LABYRINTH:"''"MAZES IN 3D..."'''
3 REM-BY D.DINI 6/10/80
5P.$21
10 DIMVV1
15 P=#2800
20[
30:VV0 JSR#FFE3;STA#80;RTS;]
40 P.$6,"DRUK TOETS"';LINK #FFE3;PRINT $12;GOSUB 4110
100 P.$12,"MAZE SIZE"'
110 INPUT"WIDTH"I,"HEIGHT"H
120 DIMA(I*H-1),B(I*H-1)
130 FORX=0TO(I*H-1);A?X=9;NEXT
135 CLEAR0
140 Y=ABSRND%H;L=0;X=-1;M=Y;D=1;E=D
145 MOVE(X*2+2),(Y*2)
150 R=0;IF ABSRND%2;GOTOa
160 D=ABSRND%4
165aV=0;W=0
170 GOTO(180+D)
180 W=1;G.b
181 V=1;G.b
182 W=-1;G.b
183 V=-1
190bIFX+V>I-1 ORX+V<0ORY+W>H-1 ORY+W<0;R=R+1;GOTOt
200 IFA?((X+V)*H+Y+W)<>9;R=R+1;GOTOt
210 X=X+V;Y=Y+W
220 A?(X*H+Y)=D;GOSUBu
225 DRAW(X*2+2),(Y*2)
230 GOTO150
240tIFR>3;GOTOe
250 IFR>2;P=D;GOTOc
260 IFR>1;K=D;P=9;GOTOc
270 J=D;K=9;P=9
280cDO D=ABSRND%4
290 UNTIL D<>J AND D<>K AND D<>P
300 GOTOa
400eD=A?(X*H+Y);D=D+4;A?(X*H+Y)=D
405 GOSUB3000
410 D=D-4
420 V=0;W=0
430 GOTO(440+D)
440 W=-1;G.h
441 V=-1;G.h
442 W=1;G.h
443 V=1
450hX=X+V;Y=Y+W;IFX=L;IFY=M;GOTOx
455 PLOT14,(X*2+2),(Y*2);WAIT;WAIT;WAIT
456 PLOT14,(X*2+2),(Y*2)
460 IFA?((X+1)*H+Y)=9;IFX+1<I;D=1;GOTOj
470 IFA?((X-1)*H+Y)=9;IFX-1>-1;D=3;GOTOj
480 IFA?(X*H+Y+1)=9;IFY+1<H;D=0;GOTOj
490 IFA?(X*H+Y-1)=9;IFY-1>-1;D=2;GOTOj
500 D=A?(X*H+Y);GOTO 420
510jMOVE(X*2+2),(Y*2)
520 F=B?(X*H+Y)
530 IFD=0;F=F&#E;G.i
540 IFD=1;F=F&#D;G.i
550 IFD=2;F=F&#B;G.i
560 IFD=3;F=F&#7
570iB?(X*H+Y)=F
580 E=D;GOTOa
600xX=I-1
610 FORY=0TO H-1
620 IF A?(X*H+Y)>3;GOTOq
630 NEXTY
640 Y=ABSRND%H
645qMOVE(X*2+2),(Y*2)
650 DRAW((X+1)*2+2),(Y*2)
660 N=X;O=Y
670 B?(N*H+O)=B?(N*H+O)&#D
690 P.$30"MAZE COMPLETE"'
700 P."PRESS ANY KEY TO ENTER MAZE";LINK#FFE3
705 B?(L*H+M)=B?(L*H+M)|8
720 Q=1;Y=M;X=L
726 CLEAR1;G=0
730 S=0;GOSUB4050
770 MOVE34,0;DRAW34,63;MOVE94,0;DRAW94,63
810 IFF&1<>0;MOVE34,0;DRAW94,0;MOVE34,63;DRAW94,63;G=1;GOTOz
815 REM--DEPTH 2
820 MOVE46,12;DRAW46,51;MOVE82,12;DRAW82,51
830 GOSUBl
835 IFS=1;G.z
840 IFF&8<>0;MOVE46,12;DRAW34,0;MOVE46,51;DRAW34,63;GOTO860
850 MOVE46,12;DRAW34,12;MOVE46,51;DRAW34,51
860 IFF&2<>0;MOVE82,12;DRAW94,0;MOVE82,51;DRAW94,63;GOTO880
870 MOVE82,12;DRAW94,12;MOVE82,51;DRAW94,51
880 IFF&1<>0;MOVE46,12;DRAW82,12;MOVE46,51;DRAW82,51;GOTOz
890 REM-DEPTH3
900 MOVE54,20;DRAW54,43;MOVE74,20;DRAW74,43
910 GOSUBl
915 IFS=1;GOTOz
920 IFF&8<>0;MOVE54,20;DRAW46,12;MOVE54,43;DRAW46,51;GOTO940
930 MOVE54,43;DRAW46,43;MOVE54,20;DRAW46,20
940 IFF&2<>0;MOVE74,20;DRAW82,12;MOVE74,43;DRAW82,51;GOTO960
950 MOVE74,20;DRAW82,20;MOVE74,43;DRAW82,43
960 IFF&1<>0;MOVE54,20;DRAW74,20;MOVE54,43;DRAW74,43;GOTOz
970 REM-DEPTH4
980 MOVE58,24;DRAW58,39;MOVE70,24;DRAW70,39
990 GOSUBl
995 IFS=1;GOTOz
1000 IFF&8<>0;MOVE58,24;DRAW54,20;MOVE58,39;DRAW54,43;GOTO1020
1010 MOVE58,39;DRAW54,39;MOVE58,24;DRAW54,24
1020 IFF&2<>0;MOVE70,24;DRAW74,20;MOVE70,39;DRAW74,43;GOTO1040
1030 MOVE70,39;DRAW74,39;MOVE70,24;DRAW74,24
1040 IFF&1<>0;MOVE58,24;DRAW70,24;MOVE58,39;DRAW70,39;GOTOz
1050 MOVE61,27;DRAW61,36;MOVE67,27;DRAW67,36
1060 GOSUBl
1065 IFS=1;G.z
1070 IFF&8<>0;MOVE61,27;DRAW58,24;MOVE61,36;DRAW58,39;GOTO1090
1080 MOVE61,27;DRAW58,27;MOVE61,36;DRAW58,36
1090 IFF&2<>0;MOVE67,27;DRAW70,24;MOVE67,36;DRAW70,39;GOTO1110
1100 MOVE67,27;DRAW70,27;MOVE67,36;DRAW70,36
1110 IFF&1<>0;MOVE61,36;DRAW67,36;MOVE61,27;DRAW67,27
1120zLINKVV0;Z=?#80
1122 IFZ<>CH"H";GOTO1130
1123 P.$12"YOU ARE AT "X","Y
1124 P.'"FACING "
1125 IFQ=0;P."DUE NORTH"'
1126 IFQ=1;P."DUE EAST"'
1127 IFQ=2;P."DUE SOUTH"'
1128 IFQ=3;P."DUE WEST"'
1129 P."PRESS ANY KEY TO CONTINUE";LINK#FFE3;GOTOp
1130 IFZ<>CH"R";G.1140
1135 Q=Q+1;IFQ=4;Q=0
1136 G.p
1140 IFZ<>CH"L";GOTO1150
1145 Q=Q-1;IFQ=-1;Q=3
1146 G.p
1150 IFZ=CH"F"ANDG=0;GOTOf
1155 P.$7;GOTOz
1160fGOTO(1170+Q)
1170 Y=Y+1;GOTOp
1171 X=X+1;GOTOp
1172 Y=Y-1;GOTOp
1173 X=X-1
1175pL=X;M=Y;IFL>N;IFO=M;P.$12"WELL DONE!"''"DRUK TOETS"'
1176 IF L>N;IF O=M;LINK #FFE3;GOTO 100
1190 CLEAR1;G=0;GOTO730
2000uIFD/2*2=D;B?(X*H+Y)=#A;GOTO2020
2010 B?(X*H+Y)=#5
2020 IFD=E;RET.
2030 IFE=0IFD=1;F=9;GOTOw
2040 IFE=3IFD=2;F=9;GOTOw
2050 IFE=0IFD=3;F=3;GOTOw
2060 IFE=1IFD=2;F=3;GOTOw
2070 IFE=2IFD=3;F=6;GOTOw
2080 IFE=1IFD=0;F=6;GOTOw
2090 IFE=3IFD=0;F=#C;GOTOw
2100 IFE=2IFD=1;F=#C;GOTOw
2110wB?((X-V)*H+Y-W)=F;E=D;RET.
2999 REM-BLOCK OFF DEAD END
3000 F=B?(X*H+Y)
3010 IFD=4;F=F|1;GOTOs
3020 IFD=5;F=F|2;GOTOs
3030 IFD=6;F=F|4;GOTOs
3040 F=F|8
3060sB?(X*H+Y)=F;RET.
4000lV=0;W=0;S=0
4010 GOTO(4020+Q)
4020 W=1;GOTOk
4021 V=1;GOTOk
4022 W=-1;GOTOk
4023 V=-1;GOTOk
4040kL=L+V;M=M+W
4045 IFL=N+1ANDM=0;F=10;S=1;RET.
4050 F=B?(L*H+M)
4055 U=Q;T=F
4060mC=F
4070 IFU=0;R.
4080 F=F/2;IFC&1<>0;F=F+8
4100 U=U-1;GOTOm
4110 P."THIS PROGRAM GENERATES A RANDOM MAZE OF SPECIFIED DIM"
4120 P."ENSIONS, THEN PLOTS IT IN 3D AS THOUGH YOU WERE ACTUAL"
4130 P."LY INSIDE IT. YOU THEN HAVE TO FIND YOUR WAY OUT BY "
4140 P."MOVING FORWARD (KEY F), OR BY TURNING RIGHT (KEY R) OR"
4150 P." LEFT (KEY L). PRESSING KEY H (FOR HELP !!!) WILL GIVE"
4160 P." YOU YOUR CO-ORDINATES AND THE DIRECTION IN WHICH YOU "
4170 P."ARE POINTING. THE EXIT IS REPRESENTED BY TWO VERTICAL "
4180 P."POLES. THE MAXIMUM SIZE OF MAZE ALLOWED IS 19 X 18."'
4190 P."DRUK TOETS"';LINK #FFE3;RETURN
