
10 REM SIMON
15 GOSUB 510
100 DIM S(10),R(-1)
110 N=5;B=0;P=#81;;P.$21
120[;LDA #B002;LDY @#FF;LDX #80;DEX;BNE P-1
130 EOR @4;STA #B002;DEY;BNE P-11;RTS
140];P.$6
200 DO;P.$12;@=0;?#E1=0
210 P."THIS TIME, SIMON GIVES YOU "N
215 P.'"LETTERS TO REMEMBER ;"''
220 FOR L=0 TO N-1;T=65+ABSRND%26
230 S?L=T;?#806C=T-64;?#80=160-L*8;LINK#81
240 FOR I=1 TO 30;WAIT;N.;?#806C=#40
250 FOR I=1 TO 30;WAIT;N.;N.L;S?N=13
300 IN.'"THEY WERE ; "$R;IF $R=$S G.340
310 P."NO - THEY WERE ; "
320 FOR L=0 TO N-1;P.$S?L;?#80=?#80+8
330 LINK#81;WAIT;N.;B=B-1;G.350
340 P."CORRECT !"';LINK#81;WAIT;LINK#81;B=B+1
350 IF B=2;N=N+1;B=0
360 IF B=-2;N=N-1;B=0
370 IF N=2 P.''"ABYSMAL !!!"';G.500
380 INPUT''"ANOTHER "$R;IF ?R=CH"N" G.500
390 UNTIL N=10
400 P."WELL DONE, I GIVE UP"'
500 ?#E1=#80;END
510 P.$12,"         *************"'"         *** SIMON ***"'
511 P."         *************"''"IN DIT PROGRAMMA LAAT DE "
512 P."COMPUTER U MEERDERE LETTERS ZIEN, WELKE U MOET ONTHOUDE"
513 P."N."''"LATER MOET U DEZE LETTERS IN DEZELFDE VOLGORDE "
514 P."WEER INGEVEN."''"DRUK TOETS OM TE BEGINNEN."'
515 LINK #FFE3;RETURN
