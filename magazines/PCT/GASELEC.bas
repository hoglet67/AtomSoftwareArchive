
5 DIM F15,H15,I2,N2
6 PRINT $12
10 PRINT"THIS IS A GAS OR ELECTRIC USE"'
15 PRINT"PER QUARTER COSTS ESTIMATOR"'
20 PRINT"WHICH COSTS DO YOU WISH TO"'
25 PRINT"ESTIMATE? GAS=G, ELECTRIC=E"'
30 INPUT $I
40 IF $I ="E" THEN GOTO 80
50 IF $I ="G" THEN GOTO 110
60 PRINT"WRONG ENTRY, TRY AGAIN"'
70 GOTO 30
80 $F="AN ELECTRIC"
90 $H="UNIT"
100 GOTO 140
110 $F = "A GAS"
120 $H = "THERM"
130 GOTO 140
140 PRINT"THIS IS "$F" COSTS PER QUARTER"'
142 PRINT"YEAR ESTIMATION"'
145 PRINT';PRINT'
150 PRINT"ENTER PRICE PER "$H" IN PENCE"'
160 INPUT A
170 IF $I="E" THEN GOTO 200
180 PRINT"ENTER CALORIFIC VALUE IN BTU CU.FT."
190 INPUT G
200 PRINT"ENTER STANDING CHARGE PER"'
205 PRINT"QUARTER YEAR IN POUNDS"'
210 INPUT B
220 PRINT"ENTER PRESENT UNITS READING AS"'
225 PRINT"ON LAST BILL"'
230 INPUT C
240 PRINT"ENTER CURRENT UNITS READING AS"'
245 PRINT"ON METER"'
250 INPUT D
260 PRINT"ENTER NUMBER OF DAYS SINCE"
265 PRINT"OFFICIAL METER READING"'"2 MIN.90MAX"'
270 INPUT E
280 %Z=D-C
290 IF $I ="G" GOTO 320 
300 %X=(%Z*A)/100 
310 GOTO 340
320 %Y=(%Z*G)/1000 
330 %X=(%Y*A)/100
340 %W=%X+B
350 %T=%Z/E
360 %S=%X/E
365 IF $I ="E" GOTO 380
370 %R=%Y/E
380 %K=%T*90
385 IF $I ="E" GOTO 400
390 %L=%R*90
400 %M=%S*90
410 %J=%M+B
420 PRINT';PRINT';PRINT';PRINT'
425 PRINT"ESTIMATION IS AS FOLLOWS"'
430 FP.%Z;PRINT" UNITS USED TO DATE" '
440 IF $I="E" GOTO 460
450 FP.%Y;PRINT" THERMS USED TO DATE"'
460 PRINT"GBP ";FP.%X;PRINT" WITHOUT"'
462 PRINT"GBP ";FP.%W;PRINT" WITH ST.CH."'
470 PRINT'
480 FP.%T;PRINT" AV.DAILY UNITS USED"' 
490 IF $I="E" GOTO 510
500 FP.%R;PRINT" AV.DAILY THERMS USED"'
510 PRINT"GBP ";FP.%S;PRINT" AV.DAILY COSTS WITHOUT ST.CH."'
520 PRINT'
530 FP.%K;PRINT" ESTIMATED UNITS USE PER QUARTER"'
540 IF $I="E" GOTO 550
545 FP.%L;PRINT" ESTIMATED THERMS USE PER QUARTER"' 
550 PRINT"GBP ";FP.%M;PRINT" WITHOUT"'
555 PRINT"GBP ";FP.%J;PRINT" WITH ST.CH."'
560 PRINT';PRINT'
570 PRINT"WANT ANOTHER ESTIMATION"'
575 PRINT"ON "$F" SERVICE? Y OR N"'
580 INPUT $N
590 IF $N="Y" THEN GOTO 220 
600 IF $N="N" THEN GOTO 630 
610 PRINT"WRONG ENTRY,TRY AGAIN"'
620 GOTO 580
630 PRINT"WANT AN ESTIMATION AT ALL? Y OR N"'
640 INPUT $N
650 IF $N ="Y" THEN GOTO 20 
655 IF $N = "N" THEN GOTO 680 
660 PRINT"WRONG ENTRY,TRY AGAIN"'
670 GOTO 640
680 END
