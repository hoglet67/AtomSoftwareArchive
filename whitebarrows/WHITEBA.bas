   10P.$12"THE WHITE BARROWS"'
   20N=42;@=0
   30DIMAA(N+1),BB4,Q10,A170
   40GOS.10000
   50GOS.9000
   60GOS.8000
   70F.X=1TO10
   80GOS.7000
   90N.
  100F.I=1TO3
  110X=10;GOS.7000
  120N.
  130X=11;GOS.7000
  140X=0;GOS.7000;Y=R
  150GOS.7000;C=R
  160M=500;L=0
  170W=0;K=0;GOS.6000
  180P.'"cREATE WALL, mOVE OR vIEW "
  190IN.$Q
  200IF$Q="V";GOS.5000;G.240
  210IF$Q="C";GOS.4000;G.240
  220IF$Q="M";GOS.3000;G.240
  230G.190
  240IF AAY=0 OR K=1 OR AAY=10 OR AAY=11 OR W=1;G.280
  250P."THE ";Z=Y;GOS.2000 
  260P."WAKES UP...AND SUDDENLY ATTACKS !"'
  270G.390
  280IF M>0;G.320
  285P."YOU HAVE USED UP ALL YOUR MAGIC..."'
  290P."YOUR STAFF IS NOW USELESS AND"'
  300P."...THE EVIL FORCE ATTACKS !"'
  310G.390
  320IF W=0;G.170
  330P."CONGRATULATIONS YOU HAVE DEFEATED"'
  340P."THE EVIL SORCERER !"'
  350P."WOULD YOU LIKE TO TRAP"'
  360IN."ANOTHER SORCERER "$Q
  370IF$Q="Y";P.$12;G.60
  380IF$Q="N";G.999
  385G.360
  390P."OH DEAR YOU APPEAR TO BE DEAD"'
  400P."WOULD YOU LIKE TO BE REINCARNATED "$Q
  410IF$Q="";G.400
  420IF$Q<>"Y";G.999
  430IN."WITH THE SAME SORCERER "$Q
  440IF$Q<>"Y";P.$12;G.60
  450X=0;GOS.7000;Y=R
  460P.$12;G.160
  999P.$12;END
 1000 M=M-R;IF M<1;R.
 1010P."YOU HAVE USED "R" UNITS."'
 1020P."YOU HAVE "M" UNITS REMAINING."'
 1030R.
 2000 G.(2000+10*AAZ)
 2010P."RED DRAGON";S=30;R.
 2020P."GREEN DRAGON";S=25;R.
 2030P."YELLOW DRAGON";S=20;R.
 2040P."PLATINUM SERPENT";S=28;R.
 2050P."GOLD SERPENT";S=23;R.
 2060P."SILVER SERPENT";S=18;R.
 2070P."DWARF WITH A SABRE";S=24;R.
 2080P."DWARF WITH A SWORD";S=19;R.
 2090P."DWARF WITH A KNIFE";S=14;R.
 2100P."FRIENDLY GNOME";R.    
 2110P."MAGIC SPOT";R.
 3000IN."WHICH CHAMBER TO MOVE TO "P
 3010F=1
 3020F.I=0TO3
 3030IF BBI=P;F=0
 3040N.
 3050IF P=0 OR F=1;GOS.6000;G.3000
 3060IF C<>P;G.3160
 3070W=1;GOS.3470;IF F=0;G.3080
 3075W=0;X=0;GOS.7000;C=R;G.3060
 3080F.I=0TO3
 3090T=A?(C*4-3+I)-48
 3100IF T=0 OR T=Y OR AAT=10 OR T=L OR T=P;G.3120
 3110C=T;W=0;GOS.3470;IF F=1;I=5
 3120N.
 3130IF F=1;G.3075
 3140IF W=1;R.
 3150P."THE SORCERER HAS RUN OFF !"'';G.3180 
 3160IF F=1;P."THE SORCERER TROD ON THE MAGIC SPOT !"''
 3180Y=P;IF AAP=0;R.
 3190IF AAP<>11;G.3230
 3200P."YOU JUST TROD ON THE MAGIC SPOT..."''
 3210P.''
 3220X=0;GOS.7000;Y=R;R.
 3230IF AAP<>10;G.3300
 3240R=A.(R.%20)+20
 3250P."THE GNOME WILL TELL YOU WHERE THE"'
 3260P."SORCERER IS FOR "R" GOLD COINS..."'
 3270IN."AGREED "$Q 
 3280IF$Q<>"Y";R. 
 3290P.'"THE SORCERER IS IN CHAMBER "C;G.3450  
 3300K=0;Z=Y;P."A ";GOS.2000;P."...IS ABOUT TO ATTACK !"'
 3310P."YOU HAVE THREE CHOICES :"'
 3320P."1 - STUN IT"'
 3330P."2 - TRANSPORT IT"'
 3340P."3 - DESTROY IT"'
 3350IN."WHAT IS YOUR CHOICE "P
 3360IF P<1 OR P>3;G.3350
 3370IN."AMOUNT OF MAGIC "R
 3380IF R<=0 OR R>M;G.3370
 3390P.$12"THE ";GOS.2000;P."...IS ";B=0
 3400IFP=1 AND R>S+R.%(S/3);P."STUNNED !"';K=1
 3410IFP<>2 OR R<S*2+R.%S;G.3430
 3420P."TRANSPORTED"';X=AAY;AAY=0;GOS.7000
 3430IFP=3;IFR>S*4+R.%S;P."DESTROYED !"';AAY=0
 3440IFK=0;IFAAY<>0;P."DAZED !"';B=A.(R.%8)
 3450GOS.1000;IFB>2;IFM>0;G.3310
 3460R.
 3470F=0
 3480F.J=1TO3
 3490T=A?(C*4-4+J)-48
 3500IFAAT=11;F=1
 3510N.
 3520R. 
 4000P."WHICH CHAMBER DO YOU WISH TO"'
 4010IN."CREATE THE MAGIC WALL IN "L
 4020P.'
 4030IFL<1 OR L>N;GOS.6000;G.4000
 4040IFAAL<>0;G.4080
 4050P."THE MAGIC WALL EXISTS IN"'
 4060P."CHAMBER "L
 4070G.4110
 4080P."ZZAAAPPP ! CHAMBER "L" ALREADY"';L=0
 4090P."CONTAINS MAGIC, THE WALL HAS"'
 4100P."FAILED !"'
 4110P.'
 4120R=A.(R.%21)+20
 4130GOS.1000
 4140R.
 5000P."WHICH CHAMBER DO YOU WISH TO"'
 5010IN."VIEW "P
 5020F=0
 5030F.I=0TO3
 5040IF P=BBI;F=1
 5050N.
 5060IFP=0 OR F=0;G.5000
 5070P.'
 5080IFP=C;P."YOU HAVE FOUND THE SORCERER !"'
 5090IFAAP=0;P."THE CHAMBER IS EMPTY !"';G.5130
 5100P."THERE IS A "
 5110Z=P;GOS.2000
 5120P."IN CHAMBER "P"."
 5130R=A.(R.%21)+10
 5140GOS.1000
 5150R.
 6000F=0;$Q="1234"
 6010F.I=0TO3
 6020BBI=A?(Y*4-4+I)-48
 6030F=F+AA(BB(I))-(100*(BB(I)=C))
 6040N.
 6050IF F=0;G.6170
 6060P."WARNING -"''
 6070IF F<0;P."THE CHALICE IS NEAR !"'
 6080F.I=0TO3
 6090R=A.(R.%4);IFQ?R=32;G.6090
 6100Q?R=32;T=AA(BB(R))
 6110IFT>0;IFT<4;P."I CAN HEAR A DRAGON !"'
 6120IFT>3;IFT<7;P."I CAN HEAR A SERPENT HISSING !"'
 6130IFT>6;IFT<11;P."I CAN HEAR MONEY RATTLING !"'
 6140IFT=11;P."I CAN FEEL MAGIC VERY CLOSE !"'
 6150N.
 6160P.'
 6170P."TUNNELS LEAD TO "
 6180F.I=0TO3
 6190IFBBI>0;P.BBI","
 6200N.
 6210P.$8" "'
 6220P."YOU ARE CURRENTLY IN CHAMBER "Y'
 6230R.
 7000R=A.(R.%42)+1;IFAAR>0;G.7000
 7010AAR=X
 7020R.
 8000F.I=0TO42
 8010AAI=0
 8020N.
 8030R.
 9000$A="23401567189:1;<=2>?@2ABC2DEF3GHI3JKL3MNO4PQR4STU"
 9010$A+LENA="4VWX5@X05Y005>A06@C06Y006AD07CF07Y007DG08FI08Y00"
 9020$A+LENA="8GJ09IK09JL09KM0:LO0:Z00:MP0;OR0;Z00;PS0<RU0<Z00"
 9030$A+LENA="<SV0=UX0=Z00=>V0?BEHNQTW"
 9040R.
10000P.'"YOUR MISSION IS TO TRAP THE"'
10010P."EVIL SORCERER AND RETURN THE"'
10020P."MAGIC CHALICE."'
10030P."YOU HAVE BEEN GIVEN A MAGIC"'
10040P."STAFF WITH 500 UNITS OF MAGIC."'
10050P."USE IT TO OVERCOME THE"'
10060P."HAZARDS HIDDEN IN THE WHITE"'
10070P."BARROWS."''"PRESS RETURN TO BEGIN.";LI.#FFE3;P.$12
10080R.
